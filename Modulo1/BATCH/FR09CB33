      *================================================================*00001000
       IDENTIFICATION                       DIVISION.                   00002000
      *================================================================*00003000
                                                                        00004000
       PROGRAM-ID.  FR09CB33.                                           00005000
       AUTHOR.      ACACIO.                                             00006000
                                                                        00007000
      *================================================================*00008000
      *                    T R E I N A M E N T O                       *00009000
      *================================================================*00010000
      *     PROGRAMA....: FR09CB33                                     *00020000
      *     PROGRAMADOR.: ACACIO JORGE                                 *00030000
      *     ANALISTA....: IVAN SANCHES                                 *00040000
      *     DATA........: 04/05/2022                                   *00050000
      *----------------------------------------------------------------*00060000
      *     OBJETIVO: RECEBER PRODUTO E GRAVAR EM RELATORIO            *00070004
      *                                                                *00071000
      *----------------------------------------------------------------*00071102
      *                                                                *00071202
      *     ARQUIVOS....:                                              *00071302
      *       DDNAME          I/O           INCLUDE/BOOK               *00071403
      *      EARQPROD          I               B#PROD                  *00071504
      *      SARQPROD          O               B#RELAT                 *00071604
      *                                                                *00071702
      ******************************************************************00072000
                                                                        00073000
      *================================================================*00074000
       ENVIRONMENT                          DIVISION.                   00075000
      *================================================================*00076000
                                                                        00077000
      *----------------------------------------------------------------*00078000
       CONFIGURATION                        SECTION.                    00079000
      *----------------------------------------------------------------*00080000
                                                                        00081000
       SPECIAL-NAMES.                                                   00081100
           DECIMAL-POINT                    IS COMMA.                   00081200
                                                                        00081300
      *----------------------------------------------------------------*00081400
       INPUT-OUTPUT                         SECTION.                    00081500
      *----------------------------------------------------------------*00081600
                                                                        00081700
       FILE-CONTROL.                                                    00081800
                                                                        00081900
           SELECT EARQPROD ASSIGN           TO EARQPROD                 00082012
                      FILE STATUS           IS WRK-FS-EARQPROD.         00082104
                                                                        00082200
           SELECT SARQPROD ASSIGN           TO SARQPROD                 00082412
                      FILE STATUS           IS WRK-FS-SARQPROD.         00082504
                                                                        00082604
      *----------------------------------------------------------------*00082704
                                                                        00082800
      *================================================================*00082900
       DATA                                 DIVISION.                   00083000
      *================================================================*00083100
                                                                        00083200
      *----------------------------------------------------------------*00083300
       FILE                                 SECTION.                    00083400
      *----------------------------------------------------------------*00083500
                                                                        00083600
      *----------------------------------------------------------------*00083700
      *    INPUT - DADOS DO ARQUIVO DE ENTRADA(EARQPROD)               *00083804
      *                                 -  LRECL  = 070                *00083904
      *----------------------------------------------------------------*00084000
                                                                        00084100
       FD EARQPROD                                                      00084204
            RECORDING MODE IS F                                         00084300
            BLOCK CONTAINS 0 RECORDS.                                   00084400
                                                                        00084500
       01 FD-EARQPROD                       PIC X(70).                  00084604
                                                                        00084700
      *----------------------------------------------------------------*00084804
      *    INPUT - DADOS DO ARQUIVO DE ENTRADA(SARQPROD)               *00084904
      *                                 -  LRECL  = 040                *00085004
      *----------------------------------------------------------------*00085104
                                                                        00085204
       FD SARQPROD                                                      00085304
            RECORDING MODE IS F                                         00085404
            BLOCK CONTAINS 0 RECORDS.                                   00085504
                                                                        00085604
       01 FD-SARQPROD                       PIC X(40).                  00085704
                                                                        00085804
      *----------------------------------------------------------------*00085900
       WORKING-STORAGE                      SECTION.                    00086000
      *----------------------------------------------------------------*00086100
                                                                        00086204
      *----------------------------------------------------------------*00086304
       01  FILLER                      PIC  X(050)         VALUE        00086404
           '*** INICIO DA WORKING RESCAJ01 ***'.                        00086504
      *----------------------------------------------------------------*00086604
                                                                        00086900
      *----------------------------------------------------------------*00087004
       01  FILLER                      PIC  X(050)         VALUE        00087104
                '*** AREA DE ACUMULADORES ***'.                         00087204
      *----------------------------------------------------------------*00087304
                                                                        00087404
       77 ACU-LIDOS                         PIC 9(09)      VALUE ZEROS. 00087505
       77 ACU-GRAVS                         PIC 9(09)      VALUE ZEROS. 00087607
       77 ACU-INCON                         PIC 9(09)      VALUE ZEROS. 00087705
                                                                        00088505
      *----------------------------------------------------------------*00088605
       01  FILLER                      PIC  X(050)         VALUE        00088705
           '*** TESTE DE FILE STATUS ***'.                              00088805
      *----------------------------------------------------------------*00088905
                                                                        00089005
       01 WRK-FS-EARQPROD                   PIC X(02)      VALUE SPACES.00089105
       01 WRK-FS-SARQPROD                   PIC X(02)      VALUE SPACES.00089205
                                                                        00089305
      *----------------------------------------------------------------*00089405
       01  FILLER                      PIC  X(050)         VALUE        00089505
           '*** AREA DO ARQUIVO - EARQPROD ***'.                        00089605
      *----------------------------------------------------------------*00089705
                                                                        00089805
       01 WRK-EARQPROD.                                                 00089905
          05 WRK-CODIGO                     PIC 9(05)      VALUE ZEROS. 00090012
          05 WRK-PRODUTO                    PIC X(20)      VALUE SPACES.00090112
          05 WRK-FORNECEDOR                 PIC X(15)      VALUE SPACES.00090212
          05 WRK-VALOR                      PIC 9(08)V99   VALUE ZEROS. 00090312
          05 WRK-ESTOQUE                    PIC 9(05)      VALUE ZEROS. 00090412
          05 FILLER                         PIC X(15)      VALUE SPACES.00090504
                                                                        00090604
      *----------------------------------------------------------------*00090705
       01  FILLER                      PIC  X(050)         VALUE        00090805
           '*** AREA DO ARQUIVO - SARQPROD ***'.                        00090905
      *----------------------------------------------------------------*00091005
                                                                        00091105
       01 WRK-SARQPROD.                                                 00091205
          05 WRK-CODIGO                     PIC 9(05)      VALUE ZEROS. 00091305
          05 WRK-PRODUTO                    PIC X(20)      VALUE SPACES.00091405
          05 WRK-FORNECEDOR                 PIC X(15)      VALUE SPACES.00091505
                                                                        00091605
      *----------------------------------------------------------------*00091705
       01  FILLER                      PIC  X(050)         VALUE        00091805
           '*** AREA DE AUXILIARES ***'.                                00091905
      *----------------------------------------------------------------*00092005
                                                                        00092105
       77 WRK-FLAG                          PIC 9(02)      VALUE ZEROS. 00092200
       77 WRK-OPERACAO                      PIC X(14)      VALUE SPACES.00092300
       77 WRK-EARQPROD-INC                  PIC X(01)      VALUE SPACES.00092411
                                                                        00092500
      *----------------------------------------------------------------*00092605
       01  FILLER                      PIC  X(050)         VALUE        00092705
           '*** AREA DE MENSAGENS ***'.                                 00092805
      *----------------------------------------------------------------*00092905
                                                                        00093005
       77 WRK-ERRO                          PIC X(30)      VALUE SPACES.00093105
       01 WRK-MENSAGEM.                                                 00093200
          05 WRK-ABERTURA                   PIC X(13)      VALUE        00093300
             'NA ABERTURA'.                                             00093400
          05 WRK-LEITURA                    PIC X(13)      VALUE        00093500
             'NA LEITURA'.                                              00093600
          05 WRK-GRAVACAO                   PIC X(13)      VALUE        00093707
             'NA GRAVACAO'.                                             00093807
          05 WRK-FECHAMENTO                 PIC X(13)      VALUE        00093900
             'NO FECHAMENTO'.                                           00094000
                                                                        00094105
      *----------------------------------------------------------------*00094205
       01  FILLER                      PIC  X(050)         VALUE        00094305
           '*** FR09CB33 - FIM DA AREA DE WORKING ***'.                 00094405
      *----------------------------------------------------------------*00094505
                                                                        00094605
      *================================================================*00094700
       PROCEDURE                            DIVISION.                   00094800
      *================================================================*00094900
                                                                        00095000
      ******************************************************************00095100
      *                       ROTINA PRINCIPAL                         *00095200
      ******************************************************************00095300
                                                                        00095400
      *----------------------------------------------------------------*00095500
       0000-PRINCIPAL                       SECTION.                    00095600
      *----------------------------------------------------------------*00095700
                                                                        00095800
           PERFORM 1000-INICIAR                                         00095900
                                                                        00096000
           IF WRK-FLAG                      EQUAL ZEROS                 00096100
              PERFORM 2000-VERIFICAR-VAZIO                              00096200
              PERFORM 3000-PROCESSAR UNTIL WRK-FS-EARQPROD EQUAL '10'   00096312
           END-IF                                                       00096400
                                                                        00096500
           PERFORM 4000-FINALIZAR                                       00096600
           STOP RUN.                                                    00096700
                                                                        00096800
      *----------------------------------------------------------------*00096900
       0000-99-FIM.                         EXIT.                       00097000
      *----------------------------------------------------------------*00097100
                                                                        00097200
      ******************************************************************00097300
      *                    I N I C I A L I Z A R                       *00097400
      ******************************************************************00097500
                                                                        00097600
      *----------------------------------------------------------------*00097700
       1000-INICIAR                         SECTION.                    00097800
      *----------------------------------------------------------------*00097900
                                                                        00098000
           OPEN INPUT  EARQPROD                                         00098106
                OUTPUT SARQPROD                                         00098206
                                                                        00098300
           IF WRK-FS-EARQPROD              NOT EQUAL ZEROS              00098408
              MOVE 'ERRO NA ABERTURA EARQPROD'                          00098507
                                            TO WRK-ERRO                 00098607
              PERFORM 9000-TRATAR-ERRO                                  00098707
              GO                            TO 1000-99-FIM              00098807
           END-IF                                                       00098907
                                                                        00099007
           IF WRK-FS-SARQPROD              NOT EQUAL ZEROS              00099108
              MOVE 'ERRO NA ABERTURA SARQPROD'                          00099207
                                            TO WRK-ERRO                 00099307
              PERFORM 9000-TRATAR-ERRO                                  00099407
              GO                            TO 1000-99-FIM              00099507
           END-IF                                                       00099607
                                                                        00099707
           MOVE WRK-ABERTURA                TO WRK-OPERACAO             00099800
                                                                        00099900
           PERFORM 1100-TESTAR-FS.                                      00100000
                                                                        00100100
      *----------------------------------------------------------------*00100200
       1000-99-FIM.                         EXIT.                       00100300
      *----------------------------------------------------------------*00100400
                                                                        00100500
      ******************************************************************00100600
      *              T E S T A R   F I L E   S T A T U S               *00100700
      ******************************************************************00100800
                                                                        00100900
      *----------------------------------------------------------------*00101000
       1100-TESTAR-FS                       SECTION.                    00101100
      *----------------------------------------------------------------*00101200
                                                                        00101307
           PERFORM 1110-TESTAR-FS-EARQPROD                              00101407
           PERFORM 1120-TESTAR-FS-SARQPROD.                             00101509
                                                                        00101600
      *----------------------------------------------------------------*00101700
       1100-99-FIM.                         EXIT.                       00101800
      *----------------------------------------------------------------*00101900
                                                                        00102000
      ******************************************************************00102107
      *              T E S T A R   F S   E A R Q P R O D               *00102207
      ******************************************************************00102307
                                                                        00102407
      *----------------------------------------------------------------*00102507
       1110-TESTAR-FS-EARQPROD              SECTION.                    00102607
      *----------------------------------------------------------------*00102707
                                                                        00102807
           IF WRK-FS-EARQPROD               NOT EQUAL ZEROS             00102907
              MOVE 'INCONSISTENCIA NO EARQPROD'                         00103007
                                            TO WRK-ERRO                 00103107
              PERFORM 9000-TRATAR-ERRO                                  00103207
           END-IF.                                                      00103307
                                                                        00103407
      *----------------------------------------------------------------*00103507
       1110-99-FIM.                         EXIT.                       00103607
      *----------------------------------------------------------------*00103707
                                                                        00103807
      ******************************************************************00103907
      *              T E S T A R   F S   S A R Q P R O D               *00104007
      ******************************************************************00104107
                                                                        00104207
      *----------------------------------------------------------------*00104307
       1120-TESTAR-FS-SARQPROD              SECTION.                    00104407
      *----------------------------------------------------------------*00104507
                                                                        00104607
           IF WRK-FS-SARQPROD               NOT EQUAL ZEROS             00104707
              MOVE 'INCONSISTENCIA NO SARQPROD'                         00104807
                                            TO WRK-ERRO                 00104907
              PERFORM 9000-TRATAR-ERRO                                  00105007
           END-IF.                                                      00105107
                                                                        00105207
      *----------------------------------------------------------------*00105307
       1120-99-FIM.                         EXIT.                       00105407
      *----------------------------------------------------------------*00105507
                                                                        00105607
      ******************************************************************00105700
      *              V E R F I F I C A R   V A Z I O                   *00105800
      ******************************************************************00105900
                                                                        00106000
      *----------------------------------------------------------------*00106100
       2000-VERIFICAR-VAZIO                 SECTION.                    00106200
      *----------------------------------------------------------------*00106300
                                                                        00106400
           PERFORM 2100-LER-EARQPROD                                    00106507
                                                                        00106600
           IF WRK-FS-EARQPROD               EQUAL  '10'                 00106707
              IF ACU-INCON                  GREATER ZEROS               00106810
                 DISPLAY '***************** EARQPROD *****************' 00106907
                 DISPLAY '*                                          *' 00107007
                 DISPLAY '*     ARQUIVO EARQPROD CONTEM APENAS       *' 00107107
                 DISPLAY '*        REGISTROS INCONSISTENTES          *' 00107207
                 DISPLAY '*                                          *' 00107307
                 DISPLAY '*        PROCESSAMENTO ENCERRADO!          *' 00107407
                 DISPLAY '*                                          *' 00107507
                 DISPLAY '***************** EARQPROD *****************' 00107607
              ELSE                                                      00107707
                 DISPLAY '***************** EARQPROD ******************'00107807
                 DISPLAY '*                                           *'00107907
                 DISPLAY '*              WRK-DADOS VAZIO              *'00108007
                 DISPLAY '*          PROCESSAMENTO  ENCERRADO         *'00108107
                 DISPLAY '*                                           *'00108207
                 DISPLAY '***************** EARQPROD ******************'00108307
              END-IF                                                    00108407
           END-IF.                                                      00108500
                                                                        00108600
      *----------------------------------------------------------------*00108700
       2000-99-FIM.                         EXIT.                       00108800
      *----------------------------------------------------------------*00108900
                                                                        00109000
      ******************************************************************00109100
      *                     L E R    P R O D U T O                     *00109201
      ******************************************************************00109300
                                                                        00109400
      *----------------------------------------------------------------*00109500
       2100-LER-EARQPROD                    SECTION.                    00109610
      *----------------------------------------------------------------*00109700
                                                                        00109800
           READ EARQPROD                    INTO WRK-EARQPROD           00109907
                                                                        00110000
           IF WRK-FS-EARQPROD               EQUAL '10'                  00110107
              GO                            TO 2100-99-FIM              00110200
           END-IF                                                       00110300
                                                                        00110400
           MOVE WRK-LEITURA                 TO WRK-OPERACAO             00110500
                                                                        00110600
           PERFORM 1100-TESTAR-FS                                       00110700
                                                                        00110800
           ADD 1                            TO ACU-LIDOS.               00110900
                                                                        00111007
           PERFORM 2110-CONSISTIR-EARQPROD.                             00111107
                                                                        00111207
           IF WRK-EARQPROD-INC              EQUAL 'N'                   00111307
              ADD 1                         TO ACU-INCON                00111410
              GO                            TO 2100-LER-EARQPROD        00111507
           END-IF.                                                      00111607
                                                                        00111700
      *----------------------------------------------------------------*00111800
       2100-99-FIM.                         EXIT.                       00111900
      *----------------------------------------------------------------*00112000
                                                                        00112100
      ******************************************************************00112207
      *          C O N S I S T I R    E A R Q P R O D                  *00112307
      ******************************************************************00112407
                                                                        00112507
      *----------------------------------------------------------------*00112607
       2110-CONSISTIR-EARQPROD              SECTION.                    00112707
      *----------------------------------------------------------------*00112807
                                                                        00112907
           IF (WRK-CODIGO OF WRK-EARQPROD    NOT NUMERIC) OR            00113012
              (WRK-CODIGO OF WRK-EARQPROD    EQUAL ZEROS)               00113112
              MOVE 'N'                       TO WRK-EARQPROD-INC        00113207
              GO                             TO 2100-99-FIM             00113307
           END-IF                                                       00113407
                                                                        00113507
           IF (WRK-PRODUTO OF WRK-EARQPROD   IS NUMERIC   ) OR          00113612
              (WRK-PRODUTO OF WRK-EARQPROD   EQUAL SPACES )             00113712
              MOVE 'N'                       TO WRK-EARQPROD-INC        00113807
              GO                             TO 2100-99-FIM             00113907
           END-IF                                                       00114007
                                                                        00114107
           IF (WRK-FORNECEDOR OF WRK-EARQPROD IS NUMERIC   ) OR         00114212
              (WRK-FORNECEDOR OF WRK-EARQPROD EQUAL SPACES )            00114312
              MOVE 'N'                       TO WRK-EARQPROD-INC        00114707
           END-IF.                                                      00114907
                                                                        00115007
      *----------------------------------------------------------------*00115107
       2110-99-FIM.                         EXIT.                       00115212
      *----------------------------------------------------------------*00115307
                                                                        00115407
      ******************************************************************00115500
      *                       P R O C E S S A R                        *00115600
      ******************************************************************00115700
                                                                        00115800
      *----------------------------------------------------------------*00115900
       3000-PROCESSAR                       SECTION.                    00116000
      *----------------------------------------------------------------*00116100
                                                                        00116200
           MOVE WRK-EARQPROD                TO WRK-SARQPROD             00116307
                                                                        00116400
           PERFORM 3100-GRAVAR                                          00116500
                                                                        00116600
           PERFORM 2100-LER-EARQPROD.                                   00116707
                                                                        00116800
      *----------------------------------------------------------------*00116900
       3000-99-FIM.                         EXIT.                       00117000
      *----------------------------------------------------------------*00117100
                                                                        00117200
      ******************************************************************00117300
      *                  R E G I S T R O   T E L A                     *00117400
      ******************************************************************00117500
                                                                        00117600
      *----------------------------------------------------------------*00117700
       3100-GRAVAR                          SECTION.                    00117800
      *----------------------------------------------------------------*00117900
                                                                        00118000
           MOVE WRK-SARQPROD                TO FD-SARQPROD              00118107
           WRITE FD-SARQPROD                FROM WRK-SARQPROD           00118207
                                                                        00118307
           MOVE WRK-GRAVACAO                TO WRK-OPERACAO             00118407
                                                                        00118507
           PERFORM 1120-TESTAR-FS-SARQPROD                              00118612
                                                                        00118707
           ADD 1                            TO ACU-GRAVS.               00118807
                                                                        00118900
      *----------------------------------------------------------------*00119000
       3100-99-FIM.                         EXIT.                       00119100
      *----------------------------------------------------------------*00119200
                                                                        00119300
      ******************************************************************00119400
      *                     F I N A L I Z A R                          *00119500
      ******************************************************************00119600
                                                                        00119700
      *----------------------------------------------------------------*00119800
       4000-FINALIZAR                       SECTION.                    00119900
      *----------------------------------------------------------------*00120000
                                                                        00120100
           IF ACU-LIDOS                     GREATER ZEROS               00120200
              PERFORM 4100-EMITIR-TOTAIS                                00120307
           END-IF                                                       00120400
                                                                        00120500
           CLOSE EARQPROD                                               00120607
                 SARQPROD                                               00120707
                                                                        00120807
           IF WRK-FS-EARQPROD               NOT EQUAL ZEROS             00120912
             MOVE 'ERRO NO FECHAMENTO EARQPROD'                         00121007
                                            TO WRK-ERRO                 00121107
             PERFORM 9000-TRATAR-ERRO                                   00121207
             GO                             TO 4000-99-FIM              00121307
           END-IF                                                       00121407
                                                                        00121507
           IF WRK-FS-SARQPROD               NOT EQUAL ZEROS             00121612
             MOVE 'ERRO NO FECHAMENTO SARQPROD'                         00121707
                                            TO WRK-ERRO                 00121807
             PERFORM 9000-TRATAR-ERRO                                   00121907
             GO                             TO 4000-99-FIM              00122007
           END-IF                                                       00122107
                                                                        00122307
           MOVE WRK-FECHAMENTO              TO WRK-OPERACAO             00122400
                                                                        00122500
           PERFORM 1100-TESTAR-FS.                                      00122600
                                                                        00122700
      *----------------------------------------------------------------*00122800
       4000-99-FIM.                         EXIT.                       00122900
      *----------------------------------------------------------------*00123000
                                                                        00123100
      ******************************************************************00123200
      *                      R E S U L T A D O                         *00123300
      ******************************************************************00123400
      *----------------------------------------------------------------*00123500
       4100-EMITIR-TOTAIS                   SECTION.                    00123607
      *----------------------------------------------------------------*00123700
                                                                        00123800
           DISPLAY '******************* FR09CB33 ********************'  00123907
           DISPLAY '*                                               *'  00124007
           DISPLAY '*          PRODUTO - PROCESSAMENTO OK!          *'  00124107
           DISPLAY '*                                               *'  00124207
           DISPLAY '* TOTAL LIDOS: ' ACU-LIDOS                          00124300
           DISPLAY '* TOTAL GRAVS: ' ACU-GRAVS                          00124407
           DISPLAY '* TOTAL INCON: ' ACU-INCON                          00124507
           DISPLAY '*                                               *'  00124607
           DISPLAY '******************* FR09CB33 ********************'. 00124707
                                                                        00124800
      *----------------------------------------------------------------*00124900
       4100-99-FIM.                         EXIT.                       00125000
      *----------------------------------------------------------------*00125100
                                                                        00125200
                                                                        00125300
      ******************************************************************00125400
      *                   T R A T A R   E R R O                        *00125500
      ******************************************************************00125600
      *----------------------------------------------------------------*00125700
       9000-TRATAR-ERRO                     SECTION.                    00125800
      *----------------------------------------------------------------*00125900
                                                                        00126000
           DISPLAY '************ TRATAMENTO DE ERRO *************'      00126100
           DISPLAY '*                                           *'      00126200
           DISPLAY '* ' WRK-ERRO                                        00126300
           DISPLAY '* ' WRK-OPERACAO                                    00126400
           DISPLAY '*                                           *'      00126500
           DISPLAY '************ TRATAMENTO DE ERRO *************'.     00126600
           MOVE 1                           TO WRK-FLAG.                00126707
                                                                        00126800
      *----------------------------------------------------------------*00126900
       9000-99-FIM.                         EXIT.                       00127000
      *----------------------------------------------------------------*00130000
