      *================================================================*00001001
       IDENTIFICATION                            DIVISION.              00002001
      *================================================================*00003001
                                                                        00004001
       PROGRAM-ID.  FR09DB12.                                           00005001
       AUTHOR.      ACACIO.                                             00005101
                                                                        00006001
      *================================================================*00007001
      *                        TREINAMENTO                             *00008001
      *================================================================*00009001
      *     PROGRAMA....: FR09DB12                                     *00010001
      *     PROGRAMADOR.: ACACIO JORGE                                 *00020001
      *     INSTRUTOR...: IVAN SANCHES                                 *00021001
      *     EMPRESA.....: FOURSYS                                      *00022001
      *     DATA........: 09/06/2022                                   *00023001
      *----------------------------------------------------------------*00024001
      *     OBJETIVO....: LER REGISTROS DA TABELA USANDO JOIN E GRAVAR *00025001
      *                   ARQUIVO DE SAIDA                             *00026001
      *----------------------------------------------------------------*00027001
      *                                                                *00027101
      *     ARQUIVOS....:                                              *00028001
      *       DDNAME               I/O                 INCLUDE/BOOK    *00029001
      *       RELSET                O                    B#RELSET    *  00029101
      *                                                                *00029201
      *----------------------------------------------------------------*00029301
      *                                                                *00029401
      *     BASE DE DADOS:                                             *00029501
      *       DDNAME               I/O                 INCLUDE/BOOK    *00029601
      *       FOU001.FUNC           I                    D#FUNC         00029701
      *                                                                *00029801
      *----------------------------------------------------------------*00029901
      *                                                                *00030001
      *     MODULOS.....:                                              *00030101
      *       B#GLOB                                                   *00030201
      *                                                                *00030301
      *================================================================*00030401
                                                                        00031001
      *================================================================*00040001
       ENVIRONMENT                               DIVISION.              00050001
      *================================================================*00060001
                                                                        00070001
      *----------------------------------------------------------------*00070101
       CONFIGURATION                             SECTION.               00070201
      *----------------------------------------------------------------*00070301
                                                                        00070401
       SPECIAL-NAMES.                                                   00070501
           DECIMAL-POINT                         IS COMMA.              00070601
                                                                        00070701
      *----------------------------------------------------------------*00070801
       INPUT-OUTPUT                              SECTION.               00070901
      *----------------------------------------------------------------*00071001
                                                                        00071101
       FILE-CONTROL.                                                    00071201
                                                                        00071301
            SELECT RELSET ASSIGN                    TO RELSET           00071401
                   FILE STATUS IS WRK-FS-RELSET.                        00071501
                                                                        00071601
      *================================================================*00071701
       DATA                                      DIVISION.              00071801
      *================================================================*00071901
                                                                        00072001
      *----------------------------------------------------------------*00072101
       FILE                                      SECTION.               00072201
      *----------------------------------------------------------------*00072301
                                                                        00072401
       FD RELSET                                                        00072501
                                                                        00072601
           RECORDING MODE IS F                                          00072701
           LABEL RECORD IS STANDARD                                     00072801
           BLOCK CONTAINS 0 RECORDS.                                    00072901
                                                                        00073001
       01 FD-RELSET                                PIC X(124).          00073101
                                                                        00073201
      *----------------------------------------------------------------*00073301
                                                                        00073401
      *----------------------------------------------------------------*00073501
       WORKING-STORAGE                           SECTION.               00073602
      *----------------------------------------------------------------*00073701
                                                                        00073802
      *----------------------------------------------------------------*00073902
       01  FILLER                      PIC  X(050)         VALUE        00074002
           '*** INICIO DA WORKING FR09DB12 ***'.                        00074103
      *----------------------------------------------------------------*00074202
                                                                        00074301
      *----------------------------------------------------------------*00074401
       01 FILLER                                  PIC X(050)    VALUE   00074501
             '*** AREA DE VARIAVEIS DE FILE STATUS ***'.                00074601
      *----------------------------------------------------------------*00074701
                                                                        00074801
       77 WRK-FS-RELSET                           PIC 9(02).            00074901
                                                                        00075001
      *----------------------------------------------------------------*00075101
       01 FILLER                                  PIC X(050)    VALUE   00075201
             '*** AREA DE VARIAVEIS AUXILIARES ***'.                    00075301
      *----------------------------------------------------------------*00075401
                                                                        00075501
       77 WRK-INDNULL                   PIC  S9(04)    COMP VALUE ZEROS.00075601
       77 ACU-LIDOS                     PIC   9(02)    VALUE ZEROS.     00075701
       77 ACU-GRAVS                     PIC   9(02)    VALUE ZEROS.     00075801
       77 WRK-SOMA-SALR                 PIC   9(10)    VALUE ZEROS.     00075901
                                                                        00076001
      *----------------------------------------------------------------*00076101
                                                                        00076201
      *----------------------------------------------------------------*00076301
       01 FILLER                                  PIC X(050)    VALUE   00076401
             '*** AREA DO ARQUIVO DB2 ***'.                             00076501
      *----------------------------------------------------------------*00076601
                                                                        00076701
          COPY '#BRELSET'.                                              00076801
                                                                        00076901
      *----------------------------------------------------------------*00077001
                                                                        00077101
      *----------------------------------------------------------------*00077201
       01 FILLER                                  PIC X(050)    VALUE   00077301
             '*** AREA DO ARQUIVO LOGERRO2 ***'.                        00077401
      *----------------------------------------------------------------*00077501
                                                                        00077601
          COPY 'B#GLOB'.                                                00077701
                                                                        00077801
      *----------------------------------------------------------------*00077901
                                                                        00078001
      *----------------------------------------------------------------*00078101
       01 FILLER                                  PIC X(050)    VALUE   00078201
             '*** AREA DE DB2 ***'.                                     00078301
      *----------------------------------------------------------------*00078401
                                                                        00078501
           EXEC SQL                                                     00078601
              INCLUDE D#FUNC                                            00078701
           END-EXEC.                                                    00078801
                                                                        00078901
           EXEC SQL                                                     00079001
              INCLUDE #SETOR                                            00079101
           END-EXEC.                                                    00079201
                                                                        00079301
           EXEC SQL                                                     00079401
              INCLUDE SQLCA                                             00079501
           END-EXEC.                                                    00079601
                                                                        00079701
           EXEC SQL                                                     00079801
              DECLARE CFUNC CURSOR FOR                                  00079901
              SELECT ID,NOME,EMAIL,SALARIO,DESCSETOR                    00080001
               FROM FOUR001.FUNC F, FOUR001.SETOR S                     00080101
               WHERE F.SETOR = S.IDSETOR                                00080201
                WITH UR                                                 00080301
           END-EXEC.                                                    00080401
                                                                        00080501
      *----------------------------------------------------------------*00080601
                                                                        00080703
      *----------------------------------------------------------------*00080803
       01  FILLER                 PIC  X(050)         VALUE             00080903
           '*** FINAL DA WORKING FR09DB12 ***'.                         00081003
      *----------------------------------------------------------------*00081103
                                                                        00081201
      *================================================================*00081301
       PROCEDURE                                 DIVISION.              00081401
      *================================================================*00081501
                                                                        00081601
      ******************************************************************00081701
      *                     P R I N C I P A L                          *00081801
      ******************************************************************00081901
                                                                        00082001
      *----------------------------------------------------------------*00082101
       0000-PRINCIPAL                            SECTION.               00082201
      *----------------------------------------------------------------*00082301
                                                                        00082401
            PERFORM 1000-INICIAR.                                       00082501
            PERFORM 2000-PROCESSAR                                      00082601
                         UNTIL SQLCODE EQUAL 100.                       00082701
            PERFORM 3000-FINALIZAR.                                     00082801
            STOP RUN.                                                   00082901
                                                                        00083001
      *----------------------------------------------------------------*00083101
       0000-99-FIM.                             EXIT.                   00083201
      *----------------------------------------------------------------*00083301
                                                                        00083401
      ******************************************************************00083501
      *                     I N I C I A L I Z A R                      *00083601
      ******************************************************************00083701
                                                                        00083801
      *----------------------------------------------------------------*00083901
       1000-INICIAR                              SECTION.               00084001
      *----------------------------------------------------------------*00084101
                                                                        00084201
            EXEC SQL                                                    00084301
               OPEN CFUNC                                               00084401
            END-EXEC.                                                   00084501
                                                                        00084601
            MOVE '1000'                               TO WRK-POSICAO    00084701
            MOVE WRK-ABERTURA                         TO WRK-OPERACAO   00084801
                                                                        00084901
             IF(SQLCODE NOT EQUAL ZEROS           AND +100) OR          00085001
               (SQLWARN0                          EQUAL 'W')            00085101
                MOVE 'FR09DB12'                       TO WRK-PROGRAMA   00085201
                MOVE WRK-POSICAO                      TO WRK-SECAO      00085301
                MOVE WRK-OPERACAO                     TO WRK-MENSAGEM   00085401
                MOVE WRK-FS-RELSET                    TO WRK-STATUS     00085501
                MOVE SQLCODE                          TO WRK-SQLCODE    00085601
                PERFORM 9000-TRATAR-ERRO                                00085701
             ELSE                                                       00085801
                PERFORM 2100-LER-FUNCIONARIO                            00085901
             END-IF.                                                    00086001
                                                                        00086101
             IF(SQLCODE                            EQUAL +100)          00086201
             MOVE 'FR09DB12'                       TO WRK-PROGRAMA      00086301
             MOVE WRK-POSICAO                      TO WRK-SECAO         00086401
             MOVE 'ARQUIVO VAZIO'                  TO WRK-MENSAGEM      00086501
             MOVE WRK-FS-RELSET                    TO WRK-STATUS        00086601
             MOVE SQLCODE                          TO WRK-SQLCODE       00086701
             PERFORM 9000-TRATAR-ERRO                                   00086801
             END-IF.                                                    00086901
                                                                        00087001
                                                                        00087101
            OPEN OUTPUT RELSET.                                         00087201
            PERFORM 1100-TESTAR-FILE-STATUS.                            00087301
                                                                        00087401
      *----------------------------------------------------------------*00087501
       1000-99-FIM.                             EXIT.                   00087601
      *----------------------------------------------------------------*00087701
                                                                        00087801
      ******************************************************************00087901
      *               T E S T A R   F I L E   S T A T U S              *00088001
      ******************************************************************00088101
                                                                        00088201
      *----------------------------------------------------------------*00088301
       1100-TESTAR-FILE-STATUS                   SECTION.               00088401
      *----------------------------------------------------------------*00088501
                                                                        00088601
            PERFORM 1110-TESTAR-FS-RELSET.                              00088701
                                                                        00088801
      *----------------------------------------------------------------*00088901
       1100-99-FIM.                             EXIT.                   00089001
      *----------------------------------------------------------------*00089101
                                                                        00089201
      ******************************************************************00089301
      *                  T E S T A R   R E L S E T                     *00089401
      ******************************************************************00089501
                                                                        00089601
      *----------------------------------------------------------------*00089701
       1110-TESTAR-FS-RELSET                     SECTION.               00089801
      *----------------------------------------------------------------*00089901
                                                                        00090001
           IF WRK-FS-RELSET                       NOT EQUAL ZEROS       00090101
                                                                        00090201
            MOVE 'FR09DB12'                       TO WRK-PROGRAMA       00090301
            MOVE WRK-POSICAO                      TO WRK-SECAO          00090401
            MOVE WRK-OPERACAO                     TO WRK-MENSAGEM       00090501
            MOVE WRK-FS-RELSET                    TO WRK-STATUS         00090601
            PERFORM 9000-TRATAR-ERRO                                    00090701
                                                                        00090801
           END-IF.                                                      00090901
                                                                        00091001
      *----------------------------------------------------------------*00091101
       1110-99-FIM.                             EXIT.                   00091201
      *----------------------------------------------------------------*00091301
                                                                        00091401
      ******************************************************************00091501
      *                     P R O C E S S A R                          *00091601
      ******************************************************************00091701
                                                                        00091801
      *----------------------------------------------------------------*00091901
       2000-PROCESSAR                            SECTION.               00092001
      *----------------------------------------------------------------*00092101
                                                                        00092201
                IF WRK-INDNULL = -1                                     00092301
                                                                        00092401
                   MOVE SPACES                   TO DB2-EMAIL           00092501
                END-IF.                                                 00092601
                                                                        00092701
               MOVE DB2-ID                       TO RELDB2-ID           00092801
               MOVE DB2-NOME                     TO RELDB2-NOME         00092901
               MOVE DB2-EMAIL                    TO RELDB2-EMAIL        00093001
               MOVE DB2-SALARIO                  TO RELDB2-SALARIO      00093101
               MOVE DB2-DESCSETOR                TO RELDB2-DESCSETOR.   00093201
                                                                        00093301
               ADD RELDB2-SALARIO                TO WRK-SOMA-SALR.      00093401
               WRITE FD-RELSET                   FROM REG-RELDB2.       00093501
               ADD 1                             TO ACU-GRAVS.          00093601
               ADD 1                             TO ACU-LIDOS.          00093701
               PERFORM 2100-LER-FUNCIONARIO.                            00093801
                                                                        00093901
      *----------------------------------------------------------------*00094001
       2000-99-FIM.                             EXIT.                   00094101
      *----------------------------------------------------------------*00094201
                                                                        00094301
      ******************************************************************00094401
      *                 L E R   F U N C I O N A R I O                  *00094501
      ******************************************************************00094601
                                                                        00094701
      *----------------------------------------------------------------*00094801
       2100-LER-FUNCIONARIO                      SECTION.               00094901
      *----------------------------------------------------------------*00095001
                                                                        00095101
           EXEC SQL                                                     00095201
             FETCH CFUNC                                                00095301
              INTO :DB2-ID,                                             00095401
                   :DB2-NOME,                                           00095501
                   :DB2-EMAIL :WRK-INDNULL,                             00095601
                   :DB2-SALARIO,                                        00095701
                   :DB2-DESCSETOR                                       00095801
            END-EXEC.                                                   00095901
                                                                        00096001
            MOVE '2100'                          TO WRK-POSICAO         00096101
            MOVE WRK-LEITURA                     TO WRK-OPERACAO        00096201
              IF (SQLCODE                        EQUAL +100)            00096301
                                                                        00096401
               MOVE 'FR09DB12'                   TO WRK-PROGRAMA        00096501
               MOVE WRK-POSICAO                  TO WRK-SECAO           00096601
               MOVE 'FINAL DA TABELA FUNC'       TO WRK-MENSAGEM        00096701
               MOVE SQLCODE                      TO WRK-SQLCODE         00096801
               MOVE WRK-FS-RELSET                TO WRK-STATUS          00096901
                DISPLAY '-FINAL DE ARQUIVO-'                            00097001
                 PERFORM 3000-FINALIZAR                                 00097101
                                                                        00097201
              END-IF.                                                   00097301
                                                                        00097401
              IF(SQLCODE              NOT EQUAL ZEROS AND +100) OR      00097501
                (SQLWARN0                          EQUAL 'W')           00097601
                                                                        00097701
               MOVE 'FR09DB12'                     TO WRK-PROGRAMA      00097801
               MOVE WRK-POSICAO                    TO WRK-SECAO         00097901
               MOVE WRK-OPERACAO                   TO WRK-MENSAGEM      00098001
               MOVE SQLCODE                        TO WRK-SQLCODE       00098101
               MOVE WRK-FS-RELSET                  TO WRK-STATUS        00098201
               PERFORM 9000-TRATAR-ERRO                                 00098301
                                                                        00098401
              END-IF.                                                   00098501
                                                                        00098601
      *----------------------------------------------------------------*00098701
       2100-99-FIM.                             EXIT.                   00098801
      *----------------------------------------------------------------*00098901
                                                                        00099001
      ******************************************************************00099101
      *                     F I N A L I Z A R                          *00099201
      ******************************************************************00099301
                                                                        00099401
      *----------------------------------------------------------------*00099501
       3000-FINALIZAR                            SECTION.               00099601
      *----------------------------------------------------------------*00099701
                                                                        00099801
           IF ACU-LIDOS                     GREATER ZEROS               00099901
              PERFORM 4000-EMITIR-ACU                                   00100001
           END-IF.                                                      00100101
                                                                        00100201
            EXEC SQL                                                    00100301
             CLOSE CFUNC                                                00100401
            END-EXEC.                                                   00100501
                                                                        00100601
            CLOSE RELSET.                                               00100701
                                                                        00100801
                                                                        00100901
      *----------------------------------------------------------------*00101001
       3000-99-FIM.                             EXIT.                   00101101
      *----------------------------------------------------------------*00101201
                                                                        00101301
      ******************************************************************00101401
      *               E M I T I R   A C U M U L A D O R E S            *00101501
      ******************************************************************00101601
                                                                        00101701
      *----------------------------------------------------------------*00101801
       4000-EMITIR-ACU                           SECTION.               00101901
      *----------------------------------------------------------------*00102001
                                                                        00102101
           DISPLAY '*=================== FR09DB12 ====================*'00102201
           DISPLAY '*                                                 *'00102301
           DISPLAY '* ACUMULADORES:                                   *'00102401
           DISPLAY '* LIDOS...............: ' ACU-LIDOS                 00102501
           DISPLAY '* GRAVADOS............: ' ACU-GRAVS                 00102601
           DISPLAY '*                                                 *'00102701
           DISPLAY '* SOMA SALARIOS.......: ' WRK-SOMA-SALR             00102801
           DISPLAY '*                                                 *'00102901
           DISPLAY '*=================== FR09DB12 ====================*'00103001
           DISPLAY ' '.                                                 00103101
                                                                        00103201
      *----------------------------------------------------------------*00103301
       4000-99-FIM.                              EXIT.                  00103401
      *----------------------------------------------------------------*00103501
                                                                        00103601
      ******************************************************************00103701
      *                     T R A T A R   E R R O                      *00103801
      ******************************************************************00103901
                                                                        00104001
      *----------------------------------------------------------------*00104101
       9000-TRATAR-ERRO                          SECTION.               00104201
      *----------------------------------------------------------------*00104301
                                                                        00104401
           CALL 'GRVLOGDB'                       USING WRK-LOG          00104501
           GOBACK.                                                      00104601
                                                                        00104701
      *----------------------------------------------------------------*00104801
       9000-99-FIM.                             EXIT.                   00104901
      *----------------------------------------------------------------*00105001
