      *================================================================*00001000
       IDENTIFICATION                            DIVISION.              00002000
      *================================================================*00003000
                                                                        00004000
       PROGRAM-ID.  FR09DB08.                                           00005000
       AUTHOR.      ACACIO.                                             00006000
                                                                        00007000
      *================================================================*00008000
      *                    T R E I N A M E N T O                       *00009000
      *================================================================*00010000
      *     PROGRAMA....: FR09DB08                                     *00020000
      *     PROGRAMADOR.: ACACIO JORGE                                 *00030000
      *     ANALISTA....: IVAN SANCHES                                 *00040000
      *     DATA........: 07/06/2022                                   *00050000
      *----------------------------------------------------------------*00060000
      *     OBJETIVO: USO DE SQL INSERT DE REGISTRO                    *00070001
      *                                                                *00070101
      *                                                                *00070201
      *----------------------------------------------------------------*00071001
      *                                                                *00072000
      *     DATA BASE...:                                              *00073001
      *      FUNC2 - BANCO DE DADOS FUNCIONARIOS                       *00074001
      *                                                                *00075001
      *----------------------------------------------------------------*00075101
      *                                                                *00075201
      *     INCLUDES....:                                              *00075301
      *      #BKFUNC2 - BOOK DA TABELA DE FUNCIONARIOS                 *00075401
      *      #GLOG    - LAYOUT DO PROGRAMA GRAVALOG                    *00075501
      *                                                                *00075601
      ******************************************************************00075701
                                                                        00076000
      *================================================================*00077000
       ENVIRONMENT                               DIVISION.              00078000
      *================================================================*00079000
                                                                        00080000
      *----------------------------------------------------------------*00081000
       CONFIGURATION                             SECTION.               00081100
      *----------------------------------------------------------------*00081200
                                                                        00081300
       SPECIAL-NAMES.                                                   00081400
           DECIMAL-POINT                         IS COMMA.              00081500
                                                                        00081600
      *----------------------------------------------------------------*00081700
                                                                        00081800
      *================================================================*00081900
       DATA                                      DIVISION.              00082000
      *================================================================*00082100
                                                                        00082200
      *----------------------------------------------------------------*00082300
       FILE                                      SECTION.               00082400
      *----------------------------------------------------------------*00082500
                                                                        00082600
      *----------------------------------------------------------------*00082700
       WORKING-STORAGE                           SECTION.               00082800
      *----------------------------------------------------------------*00082900
                                                                        00083000
      *----------------------------------------------------------------*00083101
       01  FILLER                      PIC  X(050)         VALUE        00083201
           '*** INICIO DA WORKING FR09DB08 ***'.                        00083301
      *----------------------------------------------------------------*00083401
                                                                        00083501
      *----------------------------------------------------------------*00083601
       01  FILLER                      PIC  X(050)         VALUE        00083701
           '*** AREA DA BOOK - B#GLOB'.                                 00083801
      *----------------------------------------------------------------*00083901
                                                                        00084001
           COPY 'B#GLOB'.                                               00084101
                                                                        00084201
      *----------------------------------------------------------------*00084301
       01  FILLER                      PIC  X(050)         VALUE        00084401
           '***  AREA DE DB2 ***'.                                      00084501
      *----------------------------------------------------------------*00084600
                                                                        00084900
           EXEC SQL                                                     00085000
             INCLUDE #BKFUNC2                                           00085101
           END-EXEC.                                                    00085200
                                                                        00085300
           EXEC SQL                                                     00085400
             INCLUDE SQLCA                                              00085500
           END-EXEC.                                                    00085600
                                                                        00085700
      *----------------------------------------------------------------*00085801
       01  FILLER                      PIC  X(050)         VALUE        00085901
           '*** AREA DE AUXILIARES ***'.                                00086001
      *----------------------------------------------------------------*00086101
                                                                        00086200
       77 WRK-SQLCODE                PIC -999.                          00086301
       77 WRK-INDICADOR              PIC S9(04) COMP        VALUE ZEROS.00086401
       77 WRK-CONTA                  PIC  9(04).                        00086501
       77 WRK-POS                    PIC  9(04)             VALUE ZEROS.00086601
                                                                        00086701
       01 WRK-ID.                                                       00086801
          05 FILLER                  PIC X(10).                         00086901
          05 WRK-ID-AC               PIC 9(04).                         00087001
                                                                        00087101
       01 WRK-NOME.                                                     00087201
          05 FILLER                  PIC X(10).                         00087301
          05 WRK-NOME-AC             PIC X(30).                         00087401
                                                                        00087501
       01 WRK-SETOR.                                                    00087601
          05 FILLER                  PIC X(10).                         00087701
          05 WRK-SETOR-AC            PIC X(04).                         00087801
                                                                        00087901
       01 WRK-SALARIO.                                                  00088001
          05 FILLER                  PIC X(10).                         00088101
          05 WRK-SALARIO-AC          PIC 9(08)V99.                      00088201
                                                                        00088301
       01 WRK-DATAADM.                                                  00088401
          05 FILLER                  PIC X(10).                         00088501
          05 WRK-DATAADM-AC          PIC X(10).                         00088601
                                                                        00088701
       01 WRK-EMAIL.                                                    00088801
          05 FILLER                  PIC X(10).                         00088901
          05 WRK-EMAIL-AC            PIC X(40).                         00089001
                                                                        00089101
       01 WRK-TELEFONE.                                                 00089201
          05 FILLER                  PIC X(10).                         00089301
          05 WRK-TELEFONE-AC         PIC X(11).                         00089401
                                                                        00089501
      *----------------------------------------------------------------*00089601
       01  FILLER                      PIC  X(050)         VALUE        00089701
           '*** AREA DE ACUMULADORES ***'.                              00089801
      *----------------------------------------------------------------*00089901
                                                                        00090001
       77 ACU-GRAVS                    PIC 9(03)            VALUE ZEROS.00090101
                                                                        00090201
      *----------------------------------------------------------------*00090301
       01  FILLER                      PIC  X(050)         VALUE        00090401
           '*** FINAL DA WORKING FR09DB08 ***'.                         00090501
      *----------------------------------------------------------------*00090601
                                                                        00090701
      *================================================================*00090801
       PROCEDURE                                 DIVISION.              00090901
      *================================================================*00091001
                                                                        00091101
      ******************************************************************00091201
      *                 R O T I N A   P R I N C I P A L                *00091301
      ******************************************************************00091401
                                                                        00091501
      *----------------------------------------------------------------*00091601
       0000-PRINCIPAL                            SECTION.               00091701
      *----------------------------------------------------------------*00091801
                                                                        00091901
           PERFORM 1000-INICIAR                                         00092001
           PERFORM 2000-PROCESSAR                                       00092101
           PERFORM 3000-FINALIZAR                                       00092201
           STOP RUN.                                                    00092301
                                                                        00092401
      *----------------------------------------------------------------*00092501
       0000-99-FIM.                              EXIT.                  00092601
      *----------------------------------------------------------------*00092701
                                                                        00092801
      ******************************************************************00092901
      *                     I N I C I A L I Z A R                      *00093001
      ******************************************************************00093101
                                                                        00093201
      *----------------------------------------------------------------*00093301
       1000-INICIAR                              SECTION.               00093401
      *----------------------------------------------------------------*00093501
                                                                        00093601
           ACCEPT WRK-ID.                                               00093701
           ACCEPT WRK-NOME.                                             00093801
           ACCEPT WRK-SETOR.                                            00093901
           ACCEPT WRK-SALARIO.                                          00094001
           ACCEPT WRK-DATAADM.                                          00094101
           ACCEPT WRK-EMAIL.                                            00094201
           ACCEPT WRK-TELEFONE.                                         00094301
                                                                        00094401
      *----------------------------------------------------------------*00094501
       1000-99-FIM.                              EXIT.                  00094601
      *----------------------------------------------------------------*00094701
                                                                        00094801
                                                                        00094901
      ******************************************************************00095001
      *                         PROCESSAR                              *00095101
      ******************************************************************00095201
                                                                        00095301
      *----------------------------------------------------------------*00095401
       2000-PROCESSAR                            SECTION.               00095501
      *----------------------------------------------------------------*00095601
                                                                        00095701
           PERFORM 2100-MOVER-SAIDA.                                    00095801
                                                                        00095901
           EXEC SQL                                                     00096001
            INSERT INTO                                                 00096101
             FOUR001.FUNC2(ID,NOME,SETOR,SALARIO,DATAADM,EMAIL,TELEFONE)00096201
            VALUES(:DB2-ID,                                             00096301
                    :DB2-NOME,                                          00096401
                    :DB2-SETOR,                                         00096501
                    :DB2-SALARIO,                                       00096601
                    :DB2-DATAADM,                                       00096701
                    :DB2-EMAIL,                                         00096801
                    :DB2-TELEFONE)                                      00096901
           END-EXEC.                                                    00097001
                                                                        00097101
           EVALUATE SQLCODE                                             00097201
            WHEN 0                                                      00097301
             PERFORM 2200-DISPLAY-INSERIDO                              00097401
                                                                        00097501
            WHEN -181                                                   00097601
             DISPLAY '*===============================================*'00097701
             DISPLAY '*                                               *'00097801
             DISPLAY '* DATA FORMATO INVALIDA ' WRK-DATAADM-AC          00097901
             DISPLAY '*                                               *'00098001
             DISPLAY '*===============================================*'00098101
                                                                        00098201
            WHEN -803                                                   00098301
             DISPLAY '*===============================================*'00098401
             DISPLAY '*                                               *'00098501
             DISPLAY '* DADOS DUPLICADOS- ID JA EXISTENTE ' DB2-ID      00098601
             DISPLAY '*                                               *'00098701
             DISPLAY '*===============================================*'00098801
                                                                        00098901
            WHEN OTHER                                                  00099001
             MOVE SQLCODE    TO WRK-SQLCODE                             00099101
             DISPLAY 'ERRO... ' WRK-SQLCODE                             00099201
           END-EVALUATE.                                                00099301
                                                                        00099401
      *----------------------------------------------------------------*00099501
       2000-99-FIM.                              EXIT.                  00099601
      *----------------------------------------------------------------*00099701
                                                                        00099801
      ******************************************************************00099901
      *                   M O V E R   S A I D A                        *00100001
      ******************************************************************00100101
                                                                        00100201
      *----------------------------------------------------------------*00100301
       2100-MOVER-SAIDA                          SECTION.               00100401
      *----------------------------------------------------------------*00100501
                                                                        00100601
           MOVE WRK-ID-AC                   TO DB2-ID.                  00100701
           MOVE WRK-NOME-AC                 TO DB2-NOME.                00100801
           MOVE WRK-SETOR-AC                TO DB2-SETOR.               00100901
           MOVE WRK-SALARIO-AC              TO DB2-SALARIO.             00101001
           MOVE WRK-DATAADM-AC              TO DB2-DATAADM.             00101101
                                                                        00101201
           PERFORM VARYING WRK-CONTA        FROM 40                     00101301
                    BY -1 UNTIL WRK-CONTA   EQUAL  0                    00101401
             IF WRK-EMAIL-AC(WRK-CONTA:1)   EQUAL SPACES                00101502
                MOVE  WRK-CONTA             TO WRK-POS                  00101601
             END-IF                                                     00101801
           END-PERFORM                                                  00101901
                                                                        00102001
           MOVE WRK-POS                     TO DB2-EMAIL-LEN.           00102101
           MOVE WRK-EMAIL-AC                TO DB2-EMAIL-TEXT.          00102201
           MOVE WRK-TELEFONE-AC             TO DB2-TELEFONE.            00102301
                                                                        00102401
      *----------------------------------------------------------------*00102501
       2100-99-FIM.                              EXIT.                  00102601
      *----------------------------------------------------------------*00102701
                                                                        00102801
      ******************************************************************00102901
      *                         I N S E R I D O                        *00103001
      ******************************************************************00103101
                                                                        00103201
      *----------------------------------------------------------------*00103301
       2200-DISPLAY-INSERIDO                     SECTION.               00103401
      *----------------------------------------------------------------*00103501
                                                                        00103601
             DISPLAY '*-----------------------------------------------*'00103701
             DISPLAY '*                DADOS GRAVADOS                 *'00103801
             DISPLAY '*-----------------------------------------------*'00103901
             DISPLAY '*                                               *'00104001
             DISPLAY '* ID........: ' DB2-ID                            00104101
             DISPLAY '* NOME......: ' DB2-NOME                          00104201
             DISPLAY '* SETOR.....: ' DB2-SETOR                         00104301
             DISPLAY '* SALARIO...: ' DB2-SALARIO                       00104401
             DISPLAY '* DATAADM...: ' DB2-DATAADM                       00104501
             DISPLAY '* EMAIL.....: ' DB2-EMAIL-TEXT                    00104601
             DISPLAY '* TELEFONE..: ' DB2-TELEFONE                      00104701
             DISPLAY '*                                               *'00104801
             DISPLAY '*-----------------------------------------------*'00104901
             DISPLAY '*                FIM DA GRAVACAO                *'00105001
             DISPLAY '*-----------------------------------------------*'00105101
                                                                        00105201
             ADD 1                               TO ACU-GRAVS.          00105301
                                                                        00105401
      *----------------------------------------------------------------*00105501
       2200-99-FIM.                              EXIT.                  00105601
      *----------------------------------------------------------------*00105701
                                                                        00105801
      ******************************************************************00105901
      *                       F I N A L I Z A R                        *00106001
      ******************************************************************00106101
                                                                        00106201
      *----------------------------------------------------------------*00106301
       3000-FINALIZAR                            SECTION.               00106401
      *----------------------------------------------------------------*00106501
                                                                        00106601
           IF ACU-GRAVS                          GREATER ZEROS          00106701
              PERFORM 4000-EMITIR-ACU                                   00106801
           END-IF.                                                      00106901
                                                                        00107001
      *----------------------------------------------------------------*00107101
       3000-99-FIM.                              EXIT.                  00107201
      *----------------------------------------------------------------*00107301
                                                                        00107401
      ******************************************************************00107501
      *             E M I T I R    A C U M U L A D O R E S             *00107601
      ******************************************************************00107701
                                                                        00107801
      *----------------------------------------------------------------*00107901
       4000-EMITIR-ACU                           SECTION.               00108001
      *----------------------------------------------------------------*00108101
                                                                        00108201
           DISPLAY '*=================== FR09DB01 ====================*'00108301
           DISPLAY '*                                                 *'00108401
           DISPLAY '* ACUMULADORES:                                   *'00108501
           DISPLAY '* GRAVADOS............: ' ACU-GRAVS                 00108601
           DISPLAY '*                                                 *'00108701
           DISPLAY '*=================== FR09DB01 ====================*'00108801
           DISPLAY ' '.                                                 00108901
                                                                        00109001
      *----------------------------------------------------------------*00109101
       4000-99-FIM.                              EXIT.                  00109201
      *----------------------------------------------------------------*00109301
                                                                        00109401
      ******************************************************************00109501
      *                 T R A T A R    E R R O                         *00109601
      ******************************************************************00109701
                                                                        00109801
      *----------------------------------------------------------------*00109901
       9000-TRATAR-ERRO                          SECTION.               00110001
      *----------------------------------------------------------------*00110101
                                                                        00110201
           CALL 'GRAVALOG'                       USING WRK-LOG          00110301
           GOBACK.                                                      00110401
                                                                        00110501
      *----------------------------------------------------------------*00110601
       9000-99-FIM.                              EXIT.                  00111000
      *----------------------------------------------------------------*00120000
