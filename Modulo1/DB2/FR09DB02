      *================================================================*00001101
       IDENTIFICATION                            DIVISION.              00002000
      *================================================================*00002101
                                                                        00004000
       PROGRAM-ID. FR01DB01.                                            00005000
                                                                        00006000
      *================================================================*00007101
      *   AUTOR...........: ACACIO                                  *   00008001
      *   DATA ...........: 02/06/2022                              *   00009001
      *   OBJETIVO........: INTRODUCAO SQL USO DE SELECT            *   00010002
      *================================================================*00010101
                                                                        00012000
      *================================================================*00013101
       ENVIRONMENT                               DIVISION.              00014000
      *================================================================*00014101
                                                                        00015100
      *----------------------------------------------------------------*00015301
       CONFIGURATION                             SECTION.               00015401
      *----------------------------------------------------------------*00015501
                                                                        00015601
       SPECIAL-NAMES.                                                   00015700
           DECIMAL-POINT IS COMMA.                                      00015800
                                                                        00015900
      *================================================================*00016001
       DATA                                      DIVISION.              00016101
      *================================================================*00016201
                                                                        00017000
      *----------------------------------------------------------------*00018101
       WORKING-STORAGE                           SECTION.               00019001
      *----------------------------------------------------------------*00019101
                                                                        00020101
      *----------------------------------------------------------------*00020201
      *                D E C L A R A N D O   S Q L                     *00020301
      *----------------------------------------------------------------*00020401
                                                                        00020501
           EXEC SQL                                                     00020600
             INCLUDE D#FUNC                                             00020701
           END-EXEC.                                                    00020800
                                                                        00020901
           EXEC SQL                                                     00021000
              INCLUDE SQLCA                                             00021100
           END-EXEC.                                                    00021200
                                                                        00021301
           EXEC SQL                                                     00021401
              DECLARE CFUNC CURSOR FOR                                  00021501
               SELECT * FROM FOUR001.FUNC                               00021601
                ORDER BY NOME                                           00021701
           END-EXEC.                                                    00021801
                                                                        00021901
      *--------------------- APOIO ------------------------------------*00022001
                                                                        00022101
       77 WRK-ID         PIC 9(04).                                     00022200
       77 WRK-SQLCODE    PIC -999.                                      00022300
       77 WRK-INDICATOR  PIC S9(4) COMP VALUE ZEROS.                    00022400
                                                                        00022502
       01 WRK-SOMA-SAL   PIC 9(10)      VALUE ZEROS.                    00022602
       01 WRK-MAIOR-SAL  PIC 9(10)      VALUE ZEROS.                    00022702
       01 WRK-MEDIA-SAL  PIC 9(10)      VALUE ZEROS.                    00022802
                                                                        00022902
      *----------------------------------------------------------------*00023002
      *                   AREA DE ACUMULADORES                         *00023102
      *----------------------------------------------------------------*00023202
                                                                        00023302
       77 ACU-LIDOS      PIC 9(02)      VALUE ZEROS.                    00023402
                                                                        00023502
      *================================================================*00023602
       PROCEDURE DIVISION.                                              00023702
      *================================================================*00023802
                                                                        00023902
      ******************************************************************00024002
      *                     P R I N C I P A L                          *00024102
      ******************************************************************00024202
                                                                        00024302
      *----------------------------------------------------------------*00024402
       0000-PRINCIPAL                            SECTION.               00024502
      *----------------------------------------------------------------*00024602
                                                                        00024702
           PERFORM 1000-INICIAR.                                        00024802
           PERFORM 2000-PROCESSAR UNTIL SQLCODE EQUAL 100.              00024902
           PERFORM 3000-FINALIZAR.                                      00025002
                                                                        00025102
           STOP RUN.                                                    00025202
                                                                        00025302
      *----------------------------------------------------------------*00025402
       0000-99-FIM.                              EXIT.                  00025502
      *----------------------------------------------------------------*00025602
                                                                        00025702
      ******************************************************************00025802
      *                        I N I C I A R                           *00025902
      ******************************************************************00026002
                                                                        00026102
      *----------------------------------------------------------------*00026202
       1000-INICIAR                              SECTION.               00026302
      *----------------------------------------------------------------*00026402
                                                                        00026502
           EXEC SQL                                                     00026602
             OPEN CFUNC                                                 00026702
           END-EXEC.                                                    00026802
                                                                        00026902
           EVALUATE SQLCODE                                             00027002
                                                                        00027102
            WHEN 0                                                      00027202
             PERFORM 1100-LER-FUNCIONARIO                               00027302
                                                                        00027402
            WHEN 100                                                    00027502
             DISPLAY 'SEM FUNCIONARIO'                                  00027602
                                                                        00027702
            WHEN OTHER                                                  00027802
             MOVE SQLCODE TO WRK-SQLCODE                                00027902
             DISPLAY 'ERRO CODIGO: ' WRK-SQLCODE ' NO OPEN CURSOR'      00028002
             STOP RUN                                                   00028102
                                                                        00028202
           END-EVALUATE.                                                00028302
                                                                        00028402
      *----------------------------------------------------------------*00028502
       1000-99-FIM.                              EXIT.                  00028602
      *----------------------------------------------------------------*00028702
                                                                        00028802
      ******************************************************************00028902
      *                 L E R   F U N C I O N A R I O                  *00029002
      ******************************************************************00029102
                                                                        00029202
      *----------------------------------------------------------------*00029302
       1100-LER-FUNCIONARIO                      SECTION.               00029402
      *----------------------------------------------------------------*00029502
                                                                        00029602
           EXEC SQL                                                     00029702
             FETCH CFUNC                                                00029802
              INTO :DB2-ID,                                             00029902
                   :DB2-NOME,                                           00030002
                   :DB2-SETOR,                                          00030102
                   :DB2-SALARIO,                                        00030202
                   :DB2-DATAADM,                                        00030302
                   :DB2-EMAIL      :WRK-INDICATOR                       00030402
           END-EXEC.                                                    00030502
                                                                        00030801
           EVALUATE SQLCODE                                             00030901
            WHEN 0                                                      00031001
              CONTINUE                                                  00032102
              ADD 1                              TO ACU-LIDOS           00032202
            WHEN 100                                                    00032301
              DISPLAY '*                                  *'            00032402
              DISPLAY '* ' WRK-ID ' FINAL DE TABELA       *'            00032502
              DISPLAY '*                                  *'            00032602
                                                                        00032801
            WHEN OTHER                                                  00032901
              MOVE SQLCODE TO WRK-SQLCODE                               00033001
              DISPLAY 'ERRO ... ' WRK-SQLCODE                           00033101
           END-EVALUATE.                                                00033201
                                                                        00033301
      *----------------------------------------------------------------*00033401
       1100-99-FIM.                              EXIT.                  00033501
      *----------------------------------------------------------------*00033601
                                                                        00033702
      ******************************************************************00033802
      *                     P R O C E S S A R                          *00033902
      ******************************************************************00034002
                                                                        00034102
      *----------------------------------------------------------------*00034202
       2000-PROCESSAR                            SECTION.               00034302
      *----------------------------------------------------------------*00034402
                                                                        00034502
           DISPLAY '*============ FR09DB01 ============*'               00034602
           DISPLAY '*                                  *'               00034702
           DISPLAY '* ID.......: ' DB2-ID                               00034802
           DISPLAY '* NOME.....: ' DB2-NOME                             00034902
           DISPLAY '* SETOR....: ' DB2-SETOR                            00035002
           DISPLAY '* SALARIO..: ' DB2-SALARIO                          00035102
           DISPLAY '* DATAADM..: ' DB2-DATAADM                          00035202
           DISPLAY '*                                  *'               00035302
           DISPLAY '*============ FR09DB01 ============*'               00035402
           IF WRK-INDICATOR = 0                                         00035502
              DISPLAY '*                                  *'            00035602
              DISPLAY '* EMAIL....: ' DB2-EMAIL                         00035702
              DISPLAY '*                                  *'            00035802
           ELSE                                                         00035902
              DISPLAY '*                                  *'            00036002
              DISPLAY '* SEM EMAIL!                       *'            00036102
              DISPLAY '*                                  *'            00036202
           END-IF                                                       00036302
                                                                        00036402
           IF DB2-SALARIO                        GREATER WRK-MAIOR-SAL  00036502
              MOVE DB2-SALARIO                   TO WRK-MAIOR-SAL       00036602
           END-IF                                                       00036702
                                                                        00036802
           COMPUTE WRK-SOMA-SAL = WRK-SOMA-SAL + DB2-SALARIO            00036902
                                                                        00037002
           PERFORM 1100-LER-FUNCIONARIO.                                00037102
                                                                        00037202
      *----------------------------------------------------------------*00037302
       2000-99-FIM.                              EXIT.                  00037402
      *----------------------------------------------------------------*00037502
                                                                        00037602
      ******************************************************************00037702
      *                       F I N A L I Z A R                        *00037802
      ******************************************************************00037902
                                                                        00038002
      *----------------------------------------------------------------*00038102
       3000-FINALIZAR                            SECTION.               00038202
      *----------------------------------------------------------------*00038302
                                                                        00038401
           EXEC SQL                                                     00038502
             CLOSE CFUNC                                                00038602
           END-EXEC.                                                    00038702
                                                                        00038802
           IF ACU-LIDOS                          GREATER ZEROS          00038902
              PERFORM 4000-EMITIR-ACU                                   00039002
           END-IF.                                                      00039102
                                                                        00039202
      *----------------------------------------------------------------*00039302
       3000-99-FIM.                              EXIT.                  00039402
      *----------------------------------------------------------------*00039502
                                                                        00040002
      *----------------------------------------------------------------*00050002
       4000-EMITIR-ACU                           SECTION.               00060002
      *----------------------------------------------------------------*00070002
                                                                        00071002
                                                                        00071102
           COMPUTE WRK-MEDIA-SAL = WRK-SOMA-SAL / ACU-LIDOS             00071202
                                                                        00071302
           DISPLAY '*=================== FR09DB01 ====================*'00072002
           DISPLAY '*                                                 *'00073002
           DISPLAY '* ACUMULADORES:                                   *'00073102
           DISPLAY '* LIDOS...............: ' ACU-LIDOS                 00073202
           DISPLAY '*                                                 *'00073302
           DISPLAY '* MAIOR SALARIO.......: ' WRK-MAIOR-SAL             00073402
           DISPLAY '* SOMA TOTAL..........: ' WRK-SOMA-SAL              00073502
           DISPLAY '* MEDIA DOS SALARIOS..: ' WRK-MEDIA-SAL             00073602
           DISPLAY '*                                                 *'00073702
           DISPLAY '*=================== FR09DB01 ====================*'00074002
           DISPLAY ' '.                                                 00074102
                                                                        00075002
      *----------------------------------------------------------------*00080002
       4000-99-FIM.                              EXIT.                  00090002
      *----------------------------------------------------------------*00100002
