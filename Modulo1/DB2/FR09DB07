      *================================================================*00001001
       IDENTIFICATION                            DIVISION.              00002001
      *================================================================*00003001
                                                                        00004001
       PROGRAM-ID. FR09DB07.                                            00005005
       AUTHOR.     ACACIO.                                              00005101
                                                                        00006001
      *================================================================*00012001
      *                    T R E I N A M E N T O                       *00012101
      *================================================================*00012201
      *                                                                *00012301
      *     PROGRAMA....: FR09DB07                                     *00012401
      *     PROGRAMADOR.: ACACIO JORGE                                 *00012501
      *     ANALISTA....: IVAN SANCHES                                 *00012601
      *     DATA........: 07/06/2022                                   *00012701
      *                                                                *00012801
      *----------------------------------------------------------------*00012901
      *     OBJETIVO: USO DE SQL SELECT COM TODOS OS REGISTROS         *00013001
      *                                                                *00013101
      *----------------------------------------------------------------*00013601
      *                                                                *00013701
      *     DATA BASE...:                                              *00013801
      *      FUNC2 - BANCO DE DADOS FUNCIONARIOS                       *00013901
      *                                                                *00014001
      *----------------------------------------------------------------*00014101
      *                                                                *00014201
      *     INCLUDES....:                                              *00014301
      *      #BKFUNC2 - BOOK DA TABELA DE FUNCIONARIOS                 *00014401
      *      #GLOG    - LAYOUT DO PROGRAMA GRAVALOG                    *00014501
      *                                                                *00014601
      ******************************************************************00014701
                                                                        00014801
      *================================================================*00014901
       ENVIRONMENT                               DIVISION.              00015001
      *================================================================*00015101
                                                                        00015201
      *----------------------------------------------------------------*00015301
       CONFIGURATION                             SECTION.               00015401
      *----------------------------------------------------------------*00015501
                                                                        00015601
       SPECIAL-NAMES.                                                   00015701
           DECIMAL-POINT IS COMMA.                                      00015801
                                                                        00015901
      *================================================================*00016001
       DATA                                      DIVISION.              00016101
      *================================================================*00016201
                                                                        00016301
      *----------------------------------------------------------------*00016401
       WORKING-STORAGE                           SECTION.               00016501
      *----------------------------------------------------------------*00016601
                                                                        00016701
      *----------------------------------------------------------------*00016801
       01  FILLER                      PIC  X(050)         VALUE        00016901
           '*** INICIO DA WORKING FR09DB07 ***'.                        00017001
      *----------------------------------------------------------------*00017101
                                                                        00017201
      *----------------------------------------------------------------*00017301
       01  FILLER                      PIC  X(050)         VALUE        00017401
           '*** AREA DA BOOK - B#GLOB'.                                 00017501
      *----------------------------------------------------------------*00017601
                                                                        00017701
           COPY 'B#GLOB'.                                               00017801
                                                                        00017901
      *----------------------------------------------------------------*00018701
       01  FILLER                      PIC  X(050)         VALUE        00018801
           '***  AREA DE DB2 ***'.                                      00018901
      *----------------------------------------------------------------*00019001
                                                                        00019101
           EXEC SQL                                                     00019201
             INCLUDE #BKFUNC2                                           00019302
           END-EXEC.                                                    00020001
                                                                        00020101
           EXEC SQL                                                     00020201
              INCLUDE SQLCA                                             00020301
           END-EXEC.                                                    00020401
                                                                        00020501
           EXEC SQL                                                     00020601
              DECLARE CFUNC CURSOR FOR                                  00020701
               SELECT * FROM FOUR001.FUNC2                              00020801
                ORDER BY ID                                             00020905
           END-EXEC.                                                    00021001
                                                                        00021101
      *----------------------------------------------------------------*00021301
       01  FILLER                      PIC  X(050)         VALUE        00021401
           '*** AREA DE AUXILIARES ***'.                                00021501
      *----------------------------------------------------------------*00021601
                                                                        00021701
       77 WRK-ID                       PIC 9(04).                       00021801
       77 WRK-SQLCODE                  PIC -999.                        00021901
       77 WRK-POSICAO                  PIC X(004)          VALUE SPACES.00022002
       77 WRK-OPERACAO                 PIC X(014)          VALUE SPACES.00022102
                                                                        00022201
       01 WRK-SOMA-SAL                 PIC 9(10)      VALUE ZEROS.      00022308
       01 WRK-MAIOR-SAL                PIC 9(10)      VALUE ZEROS.      00022408
       01 WRK-MEDIA-SAL                PIC 9(10)      VALUE ZEROS.      00022508
                                                                        00022608
      *----------------------------------------------------------------*00022701
       01  FILLER                      PIC  X(050)         VALUE        00022801
           '*** AREA DE NULIDADES ***'.                                 00022908
      *----------------------------------------------------------------*00023001
                                                                        00023102
       77 WRK-EMAIL-NULL               PIC S9(4) COMP VALUE ZEROS.      00023201
       77 WRK-TEL-NULL                 PIC S9(4) COMP VALUE ZEROS.      00023305
                                                                        00023501
      *----------------------------------------------------------------*00023601
       01  FILLER                      PIC  X(050)         VALUE        00023701
           '*** AREA DE ACUMULADORES ***'.                              00023801
      *----------------------------------------------------------------*00023901
                                                                        00024001
       77 ACU-LIDOS      PIC 9(02)      VALUE ZEROS.                    00024101
                                                                        00024201
      *----------------------------------------------------------------*00024301
       01  FILLER                      PIC  X(050)         VALUE        00024401
           '*** FINAL DA WORKING FR09DB07 ***'.                         00024501
      *----------------------------------------------------------------*00024601
                                                                        00024701
      *================================================================*00024801
       PROCEDURE DIVISION.                                              00024901
      *================================================================*00025001
                                                                        00025101
      ******************************************************************00025201
      *                     P R I N C I P A L                          *00025301
      ******************************************************************00025401
                                                                        00025501
      *----------------------------------------------------------------*00025601
       0000-PRINCIPAL                            SECTION.               00025701
      *----------------------------------------------------------------*00025801
                                                                        00025901
           PERFORM 1000-INICIAR.                                        00026001
           PERFORM 2000-PROCESSAR                                       00026105
                       UNTIL SQLCODE EQUAL 100.                         00026205
           PERFORM 3000-FINALIZAR.                                      00026301
                                                                        00026401
           STOP RUN.                                                    00026501
                                                                        00026601
      *----------------------------------------------------------------*00026701
       0000-99-FIM.                              EXIT.                  00026801
      *----------------------------------------------------------------*00026901
                                                                        00027001
      ******************************************************************00027101
      *                        I N I C I A R                           *00027201
      ******************************************************************00027301
                                                                        00027401
      *----------------------------------------------------------------*00027501
       1000-INICIAR                              SECTION.               00027601
      *----------------------------------------------------------------*00027701
                                                                        00027801
           EXEC SQL                                                     00027901
             OPEN CFUNC                                                 00028001
           END-EXEC.                                                    00028101
                                                                        00028202
           MOVE '1000'                           TO WRK-POSICAO         00028302
                                                                        00028402
           MOVE WRK-ABERTURA                     TO WRK-OPERACAO.       00028505
                                                                        00028602
           PERFORM 1100-TESTAR-SQL                                      00028706
                                                                        00028806
           PERFORM 1200-LER-FUNCIONARIO.                                00028906
                                                                        00029002
                                                                        00032001
      *----------------------------------------------------------------*00032101
       1000-99-FIM.                              EXIT.                  00032201
      *----------------------------------------------------------------*00032301
                                                                        00032402
      ******************************************************************00032502
      *                    T E S T A R   S Q L                         *00032602
      ******************************************************************00032702
                                                                        00032802
      *----------------------------------------------------------------*00032901
       1100-TESTAR-SQL                           SECTION.               00033001
      *----------------------------------------------------------------*00033101
                                                                        00033201
           IF (SQLCODE                       NOT EQUAL 00)              00033306
              MOVE 'FR09CB07'                   TO WRK-PROGRAMA         00033502
              MOVE WRK-SQLCODE                  TO WRK-STATUS           00033601
              MOVE WRK-OPERACAO                 TO WRK-MENSAGEM         00033701
                                                OF WRK-LOG              00033801
              MOVE WRK-POSICAO                  TO WRK-SECAO            00033901
              PERFORM 9000-TRATAR-ERRO                                  00034001
           END-IF.                                                      00034101
                                                                        00034201
      *----------------------------------------------------------------*00034301
       1100-99-FIM.                              EXIT.                  00034401
      *----------------------------------------------------------------*00034501
                                                                        00034601
      ******************************************************************00034701
      *                 L E R   F U N C I O N A R I O                  *00034801
      ******************************************************************00034901
                                                                        00035001
      *----------------------------------------------------------------*00035101
       1200-LER-FUNCIONARIO                      SECTION.               00035201
      *----------------------------------------------------------------*00035301
                                                                        00035401
           EXEC SQL                                                     00035501
             FETCH CFUNC                                                00035601
              INTO :DB2-ID,                                             00035701
                   :DB2-NOME,                                           00035801
                   :DB2-SETOR,                                          00035901
                   :DB2-SALARIO,                                        00036001
                   :DB2-DATAADM,                                        00036101
                   :DB2-EMAIL      :WRK-EMAIL-NULL,                     00036204
                   :DB2-TELEFONE   :WRK-TEL-NULL                        00036304
           END-EXEC.                                                    00036404
                                                                        00036504
                                                                        00036703
           IF (SQLCODE                  NOT EQUAL ZEROS AND +100) OR    00037005
              (SQLWARN0                 EQUAL  'W'              )       00037105
              MOVE SQLCODE              TO WRK-SQLCODE                  00037205
              PERFORM 9000-TRATAR-ERRO                                  00037305
           END-IF.                                                      00037405
                                                                        00040603
      *    MOVE '2000'                           TO WRK-POSICAO         00040707
                                                                        00040804
      *    MOVE WRK-LEITURA                      TO WRK-OPERACAO        00040907
                                                                        00041003
      *    PERFORM 1100-TESTAR-SQL.                                     00041107
                                                                        00041201
      *----------------------------------------------------------------*00041301
       1200-99-FIM.                              EXIT.                  00041401
      *----------------------------------------------------------------*00041501
                                                                        00041601
      ******************************************************************00041701
      *                     P R O C E S S A R                          *00041801
      ******************************************************************00041901
                                                                        00042001
      *----------------------------------------------------------------*00042101
       2000-PROCESSAR                            SECTION.               00042201
      *----------------------------------------------------------------*00042301
                                                                        00042401
           DISPLAY '*============ FR09DB07 ============*'               00042501
           DISPLAY '*                                  *'               00042601
           DISPLAY '* ID.......: ' DB2-ID                               00042701
           DISPLAY '* NOME.....: ' DB2-NOME                             00042801
           DISPLAY '* SETOR....: ' DB2-SETOR                            00042901
           DISPLAY '* SALARIO..: ' DB2-SALARIO                          00043001
           DISPLAY '* DATAADM..: ' DB2-DATAADM                          00043101
           DISPLAY '*                                  *'               00043201
           IF WRK-EMAIL-NULL = 0                                        00043404
              DISPLAY '*                                  *'            00043501
              DISPLAY '* EMAIL....: ' DB2-EMAIL                         00043601
              DISPLAY '*                                  *'            00043701
           ELSE                                                         00043801
              DISPLAY '*                                  *'            00043901
              DISPLAY '* SEM EMAIL!                       *'            00044001
              DISPLAY '*                                  *'            00044101
           END-IF                                                       00044201
                                                                        00044301
           IF WRK-TEL-NULL = 0                                          00044404
              DISPLAY '*                                  *'            00044504
              DISPLAY '* TELEFONE.: ' DB2-TELEFONE                      00044604
              DISPLAY '*                                  *'            00044704
           ELSE                                                         00044804
              DISPLAY '*                                  *'            00044904
              DISPLAY '* SEM TELEFONE!                    *'            00045004
              DISPLAY '*                                  *'            00045104
           END-IF                                                       00045204
           DISPLAY '*============ FR09DB07 ============*'               00045404
                                                                        00045504
           IF DB2-SALARIO                        GREATER WRK-MAIOR-SAL  00045601
              MOVE DB2-SALARIO                   TO WRK-MAIOR-SAL       00045701
           END-IF                                                       00045801
                                                                        00045901
           COMPUTE WRK-SOMA-SAL = WRK-SOMA-SAL + DB2-SALARIO            00046001
                                                                        00046106
           ADD 1                              TO ACU-LIDOS              00046206
                                                                        00046301
           PERFORM 1200-LER-FUNCIONARIO.                                00046401
                                                                        00046501
      *----------------------------------------------------------------*00046601
       2000-99-FIM.                              EXIT.                  00046701
      *----------------------------------------------------------------*00046801
                                                                        00046901
      ******************************************************************00047001
      *                       F I N A L I Z A R                        *00047101
      ******************************************************************00047201
                                                                        00047301
      *----------------------------------------------------------------*00047401
       3000-FINALIZAR                            SECTION.               00047501
      *----------------------------------------------------------------*00047601
                                                                        00047701
           EXEC SQL                                                     00047801
             CLOSE CFUNC                                                00047901
           END-EXEC.                                                    00048001
                                                                        00048101
           IF ACU-LIDOS                          GREATER ZEROS          00048201
              PERFORM 4000-EMITIR-ACU                                   00048301
           END-IF.                                                      00048401
                                                                        00048504
           IF (SQLCODE                  NOT EQUAL ZEROS AND +100) OR    00048604
              (SQLWARN0                 EQUAL  'W'              )       00048704
              MOVE SQLCODE              TO WRK-SQLCODE                  00048804
              PERFORM 9000-TRATAR-ERRO                                  00048904
           END-IF.                                                      00049004
                                                                        00049104
           IF (SQLCODE                  EQUAL +100)                     00049204
              DISPLAY '*                                  *'            00049304
              DISPLAY '* FINAL DE TABELA                  *'            00049406
              DISPLAY '*                                  *'            00049504
              GOBACK                                                    00049604
           END-IF.                                                      00049704
                                                                        00049804
           ADD 1                              TO ACU-LIDOS              00049904
                                                                        00050004
           MOVE '2000'                           TO WRK-POSICAO         00050104
                                                                        00050204
           MOVE WRK-FECHAMENTO                   TO WRK-OPERACAO        00050304
                                                                        00050404
           PERFORM 1100-TESTAR-SQL.                                     00050504
                                                                        00050601
      *----------------------------------------------------------------*00050701
       3000-99-FIM.                              EXIT.                  00050801
      *----------------------------------------------------------------*00050901
                                                                        00051009
      ******************************************************************00051109
      *             E M I T I R    A C U M U L A D O R E S             *00051209
      ******************************************************************00051309
                                                                        00051501
      *----------------------------------------------------------------*00051601
       4000-EMITIR-ACU                           SECTION.               00051701
      *----------------------------------------------------------------*00051801
                                                                        00051901
                                                                        00052001
           COMPUTE WRK-MEDIA-SAL = WRK-SOMA-SAL / ACU-LIDOS             00052101
                                                                        00052201
           DISPLAY '*=================== FR09DB07 ====================*'00053001
           DISPLAY '*                                                 *'00060001
           DISPLAY '* ACUMULADORES:                                   *'00070001
           DISPLAY '* LIDOS...............: ' ACU-LIDOS                 00071001
           DISPLAY '*                                                 *'00072001
           DISPLAY '* MAIOR SALARIO.......: ' WRK-MAIOR-SAL             00073001
           DISPLAY '* SOMA TOTAL..........: ' WRK-SOMA-SAL              00073101
           DISPLAY '* MEDIA DOS SALARIOS..: ' WRK-MEDIA-SAL             00073201
           DISPLAY '*                                                 *'00073301
           DISPLAY '*=================== FR09DB07 ====================*'00073401
           DISPLAY ' '.                                                 00073501
                                                                        00073601
      *----------------------------------------------------------------*00073701
       4000-99-FIM.                              EXIT.                  00073801
      *----------------------------------------------------------------*00073901
                                                                        00074005
      ******************************************************************00074105
      *                 T R A T A R    E R R O                         *00074205
      ******************************************************************00074305
                                                                        00074405
      *----------------------------------------------------------------*00074505
       9000-TRATAR-ERRO                          SECTION.               00074605
      *----------------------------------------------------------------*00074705
                                                                        00074805
           CALL 'GRAVALOG'                       USING WRK-LOG          00074905
           GOBACK.                                                      00075005
                                                                        00075105
      *----------------------------------------------------------------*00075205
       9000-99-FIM.                              EXIT.                  00075305
      *----------------------------------------------------------------*00075405
                                                                        00076005
