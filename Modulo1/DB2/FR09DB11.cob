      *=============================================================*   00001000
       IDENTIFICATION                            DIVISION.              00002000
      *=============================================================*   00003000
                                                                        00004000
       PROGRAM-ID. FR09DB11.                                            00005000
       AUTHOR.     ACACIO.                                              00005100
                                                                        00005200
      *================================================================*00005300
      *                    T R E I N A M E N T O                       *00005400
      *================================================================*00005500
      *     PROGRAMA....: FR09DB11                                     *00005600
      *     PROGRAMADOR.: ACACIO JORGE                                 *00005700
      *     ANALISTA....: IVAN SANCHES                                 *00005800
      *     DATA........: 09/06/2022                                   *00005900
      *----------------------------------------------------------------*00006000
      *     OBJETIVO: LEITURA NA TABELA FOUR001.FUNC E GRAVACAO DE     *00006100
      *               LAYOUT SAIDA (RELDB2)                            *00006200
      *                                                                *00006300
      *----------------------------------------------------------------*00006400
      *                                                                *00006600
      *     ARQUIVOS....:                                              *00006700
      *       DDNAME          I/O           INCLUDE/BOOK               *00006800
      *      RELDB2            O             ----------                *00006900
      *                                                                *00007000
      *----------------------------------------------------------------*00007100
      *     DATA BASE...:                                              *00007200
      *     FOUR001.FUNC - BANCO DE DADOS FUNCIONARIOS                 *00007302
      *                                                                *00007400
      *----------------------------------------------------------------*00007500
      *                                                                *00007600
      *      COPYBOOKS..:                                              *00007700
      *      #GLOG       - LAYOUT DO PROGRAMA GRAVALOG                 *00007802
      *      #FUNC       - BOOK DA TABELA DE FUNCIONARIOS              *00007902
      *      B#RELDB2    - BOOK DE RELATORIO DE FUNCIONARIOS           *00008002
      *                                                                *00008200
      ******************************************************************00008300
                                                                        00008400
      *================================================================*00008500
       ENVIRONMENT                               DIVISION.              00008600
      *================================================================*00008700
                                                                        00008800
      *----------------------------------------------------------------*00009000
       CONFIGURATION                             SECTION.               00010000
      *----------------------------------------------------------------*00011000
                                                                        00012000
       SPECIAL-NAMES.                                                   00013000
           DECIMAL-POINT IS COMMA.                                      00014000
                                                                        00015000
      *----------------------------------------------------------------*00015200
       INPUT-OUTPUT                         SECTION.                    00015300
      *----------------------------------------------------------------*00015400
                                                                        00015500
       FILE-CONTROL.                                                    00015600
                                                                        00015700
           SELECT RELDB2   ASSIGN           TO JCLRELDB                 00015800
                      FILE STATUS           IS WRK-FS-RELDB2.           00015900
                                                                        00016000
      *----------------------------------------------------------------*00016100
                                                                        00016200
      *================================================================*00016300
       DATA                                      DIVISION.              00016400
      *================================================================*00016500
                                                                        00016600
      *----------------------------------------------------------------*00016700
       FILE                                      SECTION.               00016800
      *----------------------------------------------------------------*00016900
                                                                        00017000
      *----------------------------------------------------------------*00017100
      *    INPUT - DADOS DO ARQUIVO DE SAIDA (RELDB2)                  *00017200
      *                                 -  LRECL  = 098                *00017300
      *----------------------------------------------------------------*00017400
                                                                        00017500
       FD RELDB2                                                        00017600
            RECORDING MODE IS F                                         00017700
            BLOCK CONTAINS 0 RECORDS.                                   00017800
                                                                        00017900
       01 FD-RELDB2                         PIC X(98).                  00018000
                                                                        00018100
      *----------------------------------------------------------------*00018200
       WORKING-STORAGE                           SECTION.               00018300
      *----------------------------------------------------------------*00018400
                                                                        00018500
      *----------------------------------------------------------------*00018600
       01  FILLER                      PIC  X(050)         VALUE        00018700
           '*** INICIO DA WORKING FR09DB11 ***'.                        00018800
      *----------------------------------------------------------------*00018900
                                                                        00019000
      *----------------------------------------------------------------*00019100
       01  FILLER                      PIC  X(050)         VALUE        00019200
           '*** AREA DA BOOK - B#GLODB'.                                00019301
      *----------------------------------------------------------------*00019400
                                                                        00019500
           COPY 'B#GLODB'.                                              00019601
                                                                        00019700
      *----------------------------------------------------------------*00019803
       01  FILLER                      PIC  X(050)         VALUE        00019903
           '*** AREA DA BOOK - B#GLODB'.                                00020003
      *----------------------------------------------------------------*00020103
                                                                        00020203
           COPY 'B#RELDB2'.                                             00020303
                                                                        00020403
      *----------------------------------------------------------------*00020503
       01  FILLER                      PIC  X(050)         VALUE        00020603
           '*** TESTE DE FILE STATUS ***'.                              00020703
      *----------------------------------------------------------------*00020803
                                                                        00020903
       01 WRK-FS-RELDB2                PIC 9(02)           VALUE ZEROS. 00021003
                                                                        00021203
      *----------------------------------------------------------------*00021300
       01  FILLER                      PIC  X(050)         VALUE        00021400
           '***  AREA DE DB2 ***'.                                      00021500
      *----------------------------------------------------------------*00021600
                                                                        00021700
           EXEC SQL                                                     00021800
             INCLUDE D#FUNC                                             00021903
           END-EXEC.                                                    00022000
                                                                        00022100
           EXEC SQL                                                     00022200
              INCLUDE SQLCA                                             00022300
           END-EXEC.                                                    00022400
                                                                        00022503
           EXEC SQL                                                     00022603
              DECLARE CFUNC CURSOR FOR                                  00022703
               SELECT ID, NOME, SETOR, SALARIO                          00022803
               FROM FOUR001.FUNC                                        00022903
           END-EXEC.                                                    00023103
                                                                        00023200
      *----------------------------------------------------------------*00023303
       01  FILLER                      PIC  X(050)         VALUE        00023403
           '*** AREA DE AUXILIARES ***'.                                00023503
      *----------------------------------------------------------------*00023603
                                                                        00023703
       77 WRK-ID                       PIC 9(04).                       00023803
       77 WRK-POSICAO                  PIC X(004)          VALUE SPACES.00024003
       77 WRK-OPERACAO                 PIC X(014)          VALUE SPACES.00024103
       77 WRK-AUX-SQLCODE              PIC X(003)          VALUE SPACES.00024203
                                                                        00024303
       01 WRK-SOMA-SAL                 PIC 9(10)      VALUE ZEROS.      00024403
       01 WRK-MAIOR-SAL                PIC 9(10)      VALUE ZEROS.      00024503
       01 WRK-MEDIA-SAL                PIC 9(10)      VALUE ZEROS.      00024603
                                                                        00024703
      *----------------------------------------------------------------*00024803
       01  FILLER                      PIC  X(050)         VALUE        00024903
           '*** AREA DE NULIDADES ***'.                                 00025003
      *----------------------------------------------------------------*00025103
                                                                        00025203
       77 WRK-EMAIL-NULL               PIC S9(4) COMP VALUE ZEROS.      00025303
                                                                        00025403
      *----------------------------------------------------------------*00025500
       01  FILLER                      PIC  X(050)         VALUE        00025600
           '*** AREA DE ACUMULADORES ***'.                              00025700
      *----------------------------------------------------------------*00025800
                                                                        00025900
       77 ACU-LIDOS               PIC 9(02)                 VALUE ZEROS.00026003
       77 ACU-GRAVS               PIC 9(02)                 VALUE ZEROS.00026103
                                                                        00026203
      *----------------------------------------------------------------*00026303
       01  FILLER                 PIC  X(050)         VALUE             00026403
           '*** FINAL DA WORKING FR09DB11 ***'.                         00026503
      *----------------------------------------------------------------*00026603
                                                                        00026703
      *================================================================*00026803
       PROCEDURE                                 DIVISION.              00026903
      *================================================================*00027003
                                                                        00027103
      ******************************************************************00027203
      *                     P R I N C I P A L                          *00027303
      ******************************************************************00027403
                                                                        00027503
      *----------------------------------------------------------------*00027603
       0000-PRINCIPAL                            SECTION.               00027703
      *----------------------------------------------------------------*00027803
                                                                        00027903
           PERFORM 1000-INICIAR.                                        00028003
           PERFORM 2000-PROCESSAR                                       00028103
                           UNTIL SQLCODE EQUAL 100.                     00028203
           PERFORM 3000-FINALIZAR.                                      00028303
                                                                        00028403
           STOP RUN.                                                    00028503
                                                                        00028603
      *----------------------------------------------------------------*00028703
       0000-99-FIM.                              EXIT.                  00028803
      *----------------------------------------------------------------*00028903
                                                                        00029003
      ******************************************************************00029103
      *                     I N I C I A L I Z A R                      *00029203
      ******************************************************************00029303
                                                                        00029403
      *----------------------------------------------------------------*00029503
       1000-INICIAR                              SECTION.               00029603
      *----------------------------------------------------------------*00029703
                                                                        00029803
           OPEN OUTPUT RELDB2                                           00029903
                                                                        00030003
           EXEC SQL                                                     00030103
             OPEN CFUNC                                                 00030203
           END-EXEC.                                                    00030303
                                                                        00030403
           MOVE '1000'                           TO WRK-POSICAO         00030503
                                                                        00030603
           MOVE WRK-ABERTURA                     TO WRK-OPERACAO.       00030703
                                                                        00030803
           IF (SQLCODE                       NOT EQUAL 00) AND          00030903
              (SQLWARN0                      EQUAL 'W'   )              00031005
              MOVE 'FR09DB11'                   TO WRK-PROGRAMA         00031103
              MOVE WRK-FS-RELDB2                TO WRK-STATUS           00031203
              MOVE SQLCODE                      TO WRK-SQLCODE          00031303
              MOVE WRK-OPERACAO                 TO WRK-MENSAGEM         00031403
                                                OF WRK-LOG              00031503
              MOVE WRK-POSICAO                  TO WRK-SECAO            00031603
              PERFORM 9000-TRATAR-ERRO                                  00031703
           END-IF.                                                      00031803
                                                                        00032003
           PERFORM 1100-TESTAR-FS.                                      00032103
                                                                        00032203
           PERFORM 1200-LER-FUNCIONARIO.                                00032303
                                                                        00032403
      *----------------------------------------------------------------*00032503
       1000-99-FIM.           EXIT.                                     00032603
      *----------------------------------------------------------------*00032703
                                                                        00032803
      ******************************************************************00032903
      *              T E S T A R   F I L E   S T A T U S               *00033003
      ******************************************************************00033103
                                                                        00033203
      *----------------------------------------------------------------*00033303
       1100-TESTAR-FS                       SECTION.                    00033403
      *----------------------------------------------------------------*00033503
                                                                        00033603
           PERFORM 1110-TESTAR-FS-RELDB2.                               00033703
                                                                        00033903
      *----------------------------------------------------------------*00034003
       1100-99-FIM.                         EXIT.                       00034103
      *----------------------------------------------------------------*00034203
                                                                        00034303
      ******************************************************************00034403
      *              T E S T A R   F S   R E L D B 2                   *00034503
      ******************************************************************00034603
                                                                        00034703
      *----------------------------------------------------------------*00034803
       1110-TESTAR-FS-RELDB2                SECTION.                    00034903
      *----------------------------------------------------------------*00035003
                                                                        00035103
           IF WRK-FS-RELDB2                  NOT EQUAL 00               00035203
              MOVE 'FR09DB11'                   TO WRK-PROGRAMA         00035403
              MOVE WRK-FS-RELDB2                TO WRK-STATUS           00035503
              MOVE SQLCODE                      TO WRK-AUX-SQLCODE      00035603
              MOVE WRK-AUX-SQLCODE              TO WRK-SQLCODE          00035703
              MOVE WRK-OPERACAO                 TO WRK-MENSAGEM         00035803
                                                OF WRK-LOG              00035903
              MOVE WRK-POSICAO                  TO WRK-SECAO            00036003
              PERFORM 9000-TRATAR-ERRO                                  00036103
           END-IF.                                                      00036203
                                                                        00036603
      *----------------------------------------------------------------*00036703
       1110-99-FIM.                         EXIT.                       00036803
      *----------------------------------------------------------------*00036903
                                                                        00037003
      ******************************************************************00037103
      *                 L E R   F U N C I O N A R I O                  *00037203
      ******************************************************************00037303
                                                                        00037403
      *----------------------------------------------------------------*00037503
       1200-LER-FUNCIONARIO                      SECTION.               00037603
      *----------------------------------------------------------------*00037703
                                                                        00037803
           EXEC SQL                                                     00037903
             FETCH CFUNC                                                00038003
              INTO :DB2-ID,                                             00038103
                   :DB2-NOME,                                           00038203
                   :DB2-SETOR,                                          00038303
                   :DB2-SALARIO                                         00038403
           END-EXEC.                                                    00038703
                                                                        00038803
           MOVE '1100'                           TO WRK-POSICAO         00038903
                                                                        00039003
           MOVE WRK-LEITURA                      TO WRK-OPERACAO.       00039103
                                                                        00039203
           IF (SQLCODE                       NOT EQUAL 00 AND +100) OR  00039306
              (SQLWARN0                      EQUAL 'W'   )              00039405
              MOVE 'FR09DB11'                   TO WRK-PROGRAMA         00039503
              MOVE WRK-FS-RELDB2                TO WRK-STATUS           00039603
              MOVE SQLCODE                      TO WRK-AUX-SQLCODE      00039803
              MOVE WRK-AUX-SQLCODE              TO WRK-SQLCODE          00039903
              MOVE WRK-OPERACAO                 TO WRK-MENSAGEM         00040003
                                                OF WRK-LOG              00040103
              MOVE WRK-POSICAO                  TO WRK-SECAO            00040203
              PERFORM 9000-TRATAR-ERRO                                  00040303
           END-IF.                                                      00040403
                                                                        00040503
           ADD 1                                TO ACU-LIDOS.           00040603
                                                                        00040703
      *----------------------------------------------------------------*00040803
       1200-99-FIM.           EXIT.                                     00040903
      *----------------------------------------------------------------*00041003
                                                                        00041103
      ******************************************************************00041203
      *                     P R O C E S S A R                          *00041303
      ******************************************************************00041403
                                                                        00041503
      *----------------------------------------------------------------*00041603
       2000-PROCESSAR                                        SECTION.   00041703
      *----------------------------------------------------------------*00041803
                                                                        00041903
           PERFORM 2100-MOVER-SAIDA.                                    00042003
                                                                        00042103
           PERFORM 2200-GRAVAR-SAIDA.                                   00042203
                                                                        00042603
           PERFORM 1200-LER-FUNCIONARIO.                                00042703
                                                                        00042803
      *----------------------------------------------------------------*00042903
       2000-99-FIM.                              EXIT.                  00043003
      *----------------------------------------------------------------*00043103
                                                                        00043203
      ******************************************************************00043303
      *                   M O V E R   S A I D A                        *00043403
      ******************************************************************00043503
                                                                        00043603
      *----------------------------------------------------------------*00043703
       2100-MOVER-SAIDA                          SECTION.               00043803
      *----------------------------------------------------------------*00043903
                                                                        00044003
           MOVE    DB2-ID                   TO RELDB2-ID.               00044103
           MOVE    DB2-NOME                 TO RELDB2-NOME.             00044203
           MOVE    DB2-SETOR                TO RELDB2-SETOR.            00044303
           MOVE    DB2-SALARIO              TO RELDB2-SALARIO.          00044403
                                                                        00045003
      *----------------------------------------------------------------*00045103
       2100-99-FIM.                              EXIT.                  00045203
      *----------------------------------------------------------------*00045303
                                                                        00045403
      ******************************************************************00045503
      *                   M O V E R   S A I D A                        *00045603
      ******************************************************************00045703
                                                                        00045803
      *----------------------------------------------------------------*00045903
       2200-GRAVAR-SAIDA                         SECTION.               00046003
      *----------------------------------------------------------------*00046103
                                                                        00046203
           WRITE FD-RELDB2                       FROM REG-RELDB2.       00046303
                                                                        00046403
           MOVE '2200'                           TO WRK-POSICAO         00046503
                                                                        00046603
           MOVE WRK-GRAVACAO                     TO WRK-OPERACAO.       00046703
                                                                        00046803
           PERFORM 1100-TESTAR-FS.                                      00046903
                                                                        00047003
           ADD 1                                 TO ACU-GRAVS.          00047103
                                                                        00047203
      *----------------------------------------------------------------*00047303
       2100-99-FIM.                              EXIT.                  00047403
      *----------------------------------------------------------------*00048003
                                                                        00049903
      ******************************************************************00050003
      *                     F I N A L I Z A R                          *00050103
      ******************************************************************00050203
                                                                        00050303
      *----------------------------------------------------------------*00050403
       3000-FINALIZAR                            SECTION.               00050503
      *----------------------------------------------------------------*00050603
                                                                        00050703
           IF ACU-LIDOS                          GREATER ZEROS          00050803
              PERFORM 4000-EMITIR-ACU                                   00050903
           END-IF.                                                      00051003
                                                                        00051103
           MOVE '3000'                           TO WRK-POSICAO         00051203
                                                                        00051303
           MOVE WRK-FECHAMENTO                   TO WRK-OPERACAO.       00051403
                                                                        00051503
           CLOSE RELDB2                                                 00051603
                                                                        00051703
           PERFORM 1100-TESTAR-FS.                                      00051803
                                                                        00051903
           EXEC SQL                                                     00052003
             CLOSE CFUNC                                                00052103
           END-EXEC.                                                    00052203
                                                                        00052303
           IF (SQLCODE                       NOT EQUAL 00) AND          00052403
              (SQLWARN0                      EQUAL 'W'   )              00052505
              MOVE 'FR09DB11'                   TO WRK-PROGRAMA         00052603
              MOVE WRK-FS-RELDB2                TO WRK-STATUS           00052703
              MOVE SQLCODE                      TO WRK-AUX-SQLCODE      00052803
              MOVE WRK-AUX-SQLCODE              TO WRK-SQLCODE          00052903
              MOVE WRK-OPERACAO                 TO WRK-MENSAGEM         00053003
                                                OF WRK-LOG              00053103
              MOVE WRK-POSICAO                  TO WRK-SECAO            00053203
              PERFORM 9000-TRATAR-ERRO                                  00053303
           END-IF.                                                      00053403
                                                                        00053503
      *----------------------------------------------------------------*00053603
       3000-99-FIM.                              EXIT.                  00053703
      *----------------------------------------------------------------*00053803
                                                                        00053903
      ******************************************************************00054003
      *               E M I T I R   A C U M U L A D O R E S            *00054103
      ******************************************************************00054203
                                                                        00054303
      *----------------------------------------------------------------*00054403
       4000-EMITIR-ACU                           SECTION.               00054503
      *----------------------------------------------------------------*00054603
                                                                        00054703
           DISPLAY '*=================== FR09DB11 ====================*'00054803
           DISPLAY '*                                                 *'00054903
           DISPLAY '* ACUMULADORES:                                   *'00055003
           DISPLAY '* LIDOS...............: ' ACU-LIDOS                 00055103
           DISPLAY '* GRAVADOS............: ' ACU-GRAVS                 00055203
           DISPLAY '*                                                 *'00055303
           DISPLAY '*=================== FR09DB11 ====================*'00055403
           DISPLAY ' '.                                                 00055503
                                                                        00055603
      *----------------------------------------------------------------*00055703
       4000-99-FIM.                              EXIT.                  00055803
      *----------------------------------------------------------------*00055903
                                                                        00056003
      ******************************************************************00056103
      *                     T R A T A R   E R R O                      *00056203
      ******************************************************************00056303
                                                                        00056403
      *----------------------------------------------------------------*00056503
       9000-TRATAR-ERRO                          SECTION.               00056603
      *----------------------------------------------------------------*00056703
                                                                        00056803
           CALL 'GRVLOGDB'                       USING WRK-LOG          00056903
           GOBACK.                                                      00057003
                                                                        00057103
      *----------------------------------------------------------------*00057203
       9000-99-FIM.                              EXIT.                  00058003
      *----------------------------------------------------------------*00060000
