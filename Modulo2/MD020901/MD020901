      *================================================================*00001000
       IDENTIFICATION                       DIVISION.                   00002000
      *================================================================*00003000
                                                                        00004000
       PROGRAM-ID.  MD020901.                                           00005000
       AUTHOR.      ACACIO.                                             00006000
                                                                        00007000
      *================================================================*00008000
      *                    T R E I N A M E N T O                       *00009000
      *================================================================*00010000
      *     PROGRAMA....: MD020901                                     *00020000
      *     PROGRAMADOR.: ACACIO JORGE                                 *00030000
      *     ANALISTA....: IVAN SANCHES                                 *00040000
      *     DATA........: 06/07/2022                                   *00050000
      *----------------------------------------------------------------*00060000
      *     OBJETIVO: REALIZAR A LEITURA DO ARQUIVO EVSA0407, FORMATAR *00070000
      *               OS DADOS EM UM ARQUIVO SEQUENCIAL COM REGISTROS  *00071000
      *               ONDE O SALDO FOR MAIOR QUE 0; SALDOS MENORES     *00071100
      *               DEVERAO SER DESPREZADOS NA SYSOUT                *00071200
      *                                                                *00071300
      *----------------------------------------------------------------*00071400
      *                                                                *00071500
      *     ARQUIVOS....:                                              *00071600
      *       DDNAME          I/O           INCLUDE/BOOK               *00071700
      *      EVSA0407          I              B#EVSA                   *00071800
      *      SVSA0407          O              B#SVSA                   *00071900
      *                                                                *00072000
      *----------------------------------------------------------------*00072200
      *                                                                *00072300
      *     INCLUDES....:                                              *00072400
      *      I#BV0407 - LAYOUT DE SAIDA                                *00072600
      *      B#GLOB   - BOOK DE TRATAMENTO DE ERRO                     *00072801
      *                                                                *00072901
      ******************************************************************00073001
                                                                        00073101
      *================================================================*00073201
       ENVIRONMENT                          DIVISION.                   00073301
      *================================================================*00073401
                                                                        00073501
      *----------------------------------------------------------------*00073601
       CONFIGURATION                        SECTION.                    00073701
      *----------------------------------------------------------------*00073801
                                                                        00073901
       SPECIAL-NAMES.                                                   00074001
           DECIMAL-POINT                    IS COMMA.                   00074101
                                                                        00075000
      *----------------------------------------------------------------*00076000
       INPUT-OUTPUT                         SECTION.                    00077000
      *----------------------------------------------------------------*00078000
                                                                        00079000
       FILE-CONTROL.                                                    00080000
                                                                        00081000
           SELECT EVSA0407 ASSIGN           TO JCLEVSA                  00081100
              ORGANIZATION                  IS INDEXED                  00081200
              ACCESS MODE                   IS SEQUENTIAL               00081300
              RECORD KEY                    FD-CHAVE                    00081403
              FILE STATUS                   WRK-FS-EVSA.                00081503
                                                                        00081600
           SELECT SVSA0407 ASSIGN           TO JCLSVSA                  00081703
                      FILE STATUS           IS WRK-FS-SVSA.             00081803
                                                                        00082500
      *----------------------------------------------------------------*00082600
                                                                        00082700
      *================================================================*00082800
       DATA                                 DIVISION.                   00082900
      *================================================================*00083000
                                                                        00083100
      *----------------------------------------------------------------*00083200
       FILE                                 SECTION.                    00083300
      *----------------------------------------------------------------*00083400
                                                                        00083500
      *----------------------------------------------------------------*00083600
      *    INPUT - DADOS DO ARQUIVO DE ENTRADA (EVSA0407)              *00083700
      *                                 -  LRECL  = 019                *00083800
      *----------------------------------------------------------------*00083900
                                                                        00084000
       FD EVSA0407.                                                     00084100
                                                                        00084400
       01 FD-EVSA0407.                                                  00084600
          05 FD-CHAVE.                                                  00084700
             10 FD-AGENCIA             PIC X(004).                      00084800
             10 FD-CONTA               PIC X(005).                      00084900
          05 FD-SALDO                  PIC X(010).                      00085000
                                                                        00085100
      *----------------------------------------------------------------*00085200
      *    INPUT - DADOS DO ARQUIVO DE SAIDA (SVSA0407)                *00085300
      *                                 -  LRECL  = 027                *00085400
      *----------------------------------------------------------------*00085500
                                                                        00085600
       FD SVSA0407                                                      00085700
           RECORDING MODE IS F                                          00085803
           BLOCK CONTAINS 0 RECORDS.                                    00085903
                                                                        00086000
       01 FD-SVSA0407                  PIC X(027).                      00086100
                                                                        00086400
      *----------------------------------------------------------------*00086500
       WORKING-STORAGE                      SECTION.                    00086600
      *----------------------------------------------------------------*00086700
                                                                        00086800
      *----------------------------------------------------------------*00086900
       01  FILLER                      PIC  X(050)         VALUE        00087000
           '*** INICIO DA WORKING MD020901 ***'.                        00087100
      *----------------------------------------------------------------*00087200
                                                                        00087300
      *----------------------------------------------------------------*00088300
       01  FILLER                      PIC  X(050)         VALUE        00088400
           '*** TESTE DE FILE STATUS ***'.                              00088500
      *----------------------------------------------------------------*00088600
                                                                        00088700
       01 WRK-FS-EVSA                       PIC X(02)      VALUE SPACES.00088800
       01 WRK-FS-SVSA                       PIC X(02)      VALUE SPACES.00088900
                                                                        00089000
      *----------------------------------------------------------------*00089801
       01  FILLER                      PIC  X(050)         VALUE        00089901
           '*** AREA DA INCLUDE - I#BV0407 ***'.                        00090001
      *----------------------------------------------------------------*00090101
                                                                        00090201
           COPY 'I#BV0407'.                                             00090301
                                                                        00090401
      *----------------------------------------------------------------*00090501
       01  FILLER                      PIC  X(050)         VALUE        00090601
           '*** AREA DO ARQUIVO - EVSA0407 ***'.                        00090701
      *----------------------------------------------------------------*00090801
                                                                        00090901
           COPY 'B#EVSA'.                                               00091003
                                                                        00091801
      *----------------------------------------------------------------*00091901
       01  FILLER                      PIC  X(050)         VALUE        00092001
           '*** AREA DA INCLUDE - I#BV0407 ***'.                        00092101
      *----------------------------------------------------------------*00092201
                                                                        00092301
           COPY 'B#GLOB'.                                               00092401
                                                                        00092501
      *----------------------------------------------------------------*00092601
       01  FILLER                      PIC  X(050)         VALUE        00092701
           '*** AREA DE AUXILIARES ***'.                                00092801
      *----------------------------------------------------------------*00092901
                                                                        00093001
       77 WRK-GRVLOG                   PIC X(008)   VALUE 'GRAVALOG'.   00093104
       77 WRK-POSICAO                  PIC X(004)          VALUE SPACES.00093201
       77 WRK-OPERACAO                 PIC X(014)          VALUE SPACES.00093301
       77 WRK-DATA                     PIC X(008).                      00093401
       77 WRK-EVSA0407-INC             PIC X(001)          VALUE SPACES.00093500
                                                                        00093601
      *----------------------------------------------------------------*00093701
       01  FILLER                      PIC  X(050)         VALUE        00093801
                '*** AREA DE ACUMULADORES ***'.                         00093901
      *----------------------------------------------------------------*00094001
                                                                        00094101
       77 ACU-LIDOS                         PIC 9(09)      VALUE ZEROS. 00094201
       77 ACU-GRAVS                         PIC 9(09)      VALUE ZEROS. 00094301
       77 ACU-INCON                         PIC 9(09)      VALUE ZEROS. 00094401
                                                                        00094501
                                                                        00094600
      *----------------------------------------------------------------*00094700
       01  FILLER                      PIC  X(050)         VALUE        00094800
           '*** MD020901 - FIM DA AREA DE WORKING ***'.                 00094900
      *----------------------------------------------------------------*00095000
                                                                        00095100
      *================================================================*00095200
       PROCEDURE                            DIVISION.                   00095300
      *================================================================*00095400
                                                                        00095500
      ******************************************************************00095600
      *                       ROTINA PRINCIPAL                         *00095700
      ******************************************************************00095800
                                                                        00095900
      *----------------------------------------------------------------*00096000
       0000-PRINCIPAL                       SECTION.                    00096100
      *----------------------------------------------------------------*00096200
                                                                        00096300
           PERFORM 1000-INICIAR                                         00096400
                                                                        00096500
           PERFORM 2000-VERIFICAR-VAZIO                                 00096600
                                                                        00096700
           PERFORM 3000-PROCESSAR                                       00096800
               UNTIL WRK-FS-EVSA EQUAL '10'                             00096900
                                                                        00097000
           PERFORM 4000-FINALIZAR                                       00097100
                                                                        00097200
           GOBACK.                                                      00097300
                                                                        00097400
      *----------------------------------------------------------------*00097500
       0000-99-FIM.                         EXIT.                       00097600
      *----------------------------------------------------------------*00097700
                                                                        00097800
      ******************************************************************00097900
      *                    I N I C I A L I Z A R                       *00098000
      ******************************************************************00098100
                                                                        00098200
      *----------------------------------------------------------------*00098300
       1000-INICIAR                         SECTION.                    00098400
      *----------------------------------------------------------------*00098500
                                                                        00098600
           OPEN INPUT  EVSA0407                                         00098700
                OUTPUT SVSA0407                                         00098800
                                                                        00098901
           ACCEPT WRK-DATA                  FROM DATE YYYYMMDD.         00099001
                                                                        00100000
           MOVE WRK-ABERTURA                TO WRK-OPERACAO             00100700
           MOVE '1000'                      TO WRK-POSICAO              00100801
                                                                        00100900
           PERFORM 1100-TESTAR-FS.                                      00101000
                                                                        00101100
      *----------------------------------------------------------------*00101200
       1000-99-FIM.                         EXIT.                       00101300
      *----------------------------------------------------------------*00101400
                                                                        00101500
      ******************************************************************00101600
      *              T E S T A R   F I L E   S T A T U S               *00101700
      ******************************************************************00101800
                                                                        00101900
      *----------------------------------------------------------------*00102000
       1100-TESTAR-FS                       SECTION.                    00102100
      *----------------------------------------------------------------*00102200
                                                                        00102300
           PERFORM 1110-TESTAR-FS-EVSA0407                              00102400
           PERFORM 1120-TESTAR-FS-SVSA0407.                             00102500
                                                                        00102600
      *----------------------------------------------------------------*00102700
       1100-99-FIM.                         EXIT.                       00102800
      *----------------------------------------------------------------*00102900
                                                                        00103000
      ******************************************************************00103100
      *              T E S T A R   F S   E V S A 0 4 0 7               *00103201
      ******************************************************************00103300
                                                                        00103400
      *----------------------------------------------------------------*00103500
       1110-TESTAR-FS-EVSA0407              SECTION.                    00103600
      *----------------------------------------------------------------*00103700
                                                                        00103800
           IF WRK-FS-EVSA                   NOT EQUAL '00'              00103901
              MOVE 'MD020901'                   TO WRK-PROGRAMA         00104001
              MOVE WRK-FS-EVSA                  TO WRK-STATUS           00104101
              MOVE WRK-OPERACAO                 TO WRK-MENSAGEM         00104201
                                                OF WRK-LOG              00104303
              MOVE WRK-POSICAO                  TO WRK-SECAO            00104401
              PERFORM 9000-TRATAR-ERROS                                 00104501
           END-IF.                                                      00104601
                                                                        00104700
      *----------------------------------------------------------------*00104800
       1110-99-FIM.                         EXIT.                       00104900
      *----------------------------------------------------------------*00105000
                                                                        00105100
      ******************************************************************00105200
      *              T E S T A R   F S   S V S A 0 4 0 7               *00105301
      ******************************************************************00105400
                                                                        00105500
      *----------------------------------------------------------------*00105600
       1120-TESTAR-FS-SVSA0407              SECTION.                    00105700
      *----------------------------------------------------------------*00105800
                                                                        00105900
           IF WRK-FS-SVSA                   NOT EQUAL '00'              00106001
              MOVE 'MD020901'                   TO WRK-PROGRAMA         00106101
              MOVE WRK-FS-SVSA                  TO WRK-STATUS           00106201
              MOVE WRK-OPERACAO                 TO WRK-MENSAGEM         00106301
                                                OF WRK-LOG              00106403
              MOVE WRK-POSICAO                  TO WRK-SECAO            00106501
              PERFORM 9000-TRATAR-ERROS                                 00106601
           END-IF.                                                      00106701
                                                                        00107000
      *----------------------------------------------------------------*00107100
       1120-99-FIM.                         EXIT.                       00107200
      *----------------------------------------------------------------*00107300
                                                                        00107400
      ******************************************************************00107500
      *              V E R F I F I C A R   V A Z I O                   *00107600
      ******************************************************************00107700
                                                                        00107800
      *----------------------------------------------------------------*00107900
       2000-VERIFICAR-VAZIO                 SECTION.                    00108000
      *----------------------------------------------------------------*00108100
                                                                        00108200
           PERFORM 2100-LER-EVSA0407                                    00108300
                                                                        00108400
           IF WRK-FS-EVSA                   EQUAL  '10'                 00108500
              IF ACU-INCON                  GREATER ZEROS               00108600
                 DISPLAY '***************** EVSA0407 *****************' 00108700
                 DISPLAY '*                                          *' 00108800
                 DISPLAY '*     ARQUIVO EVSA0407 CONTEM APENAS       *' 00108900
                 DISPLAY '*        REGISTROS INCONSISTENTES          *' 00109000
                 DISPLAY '*                                          *' 00109100
                 DISPLAY '*        PROCESSAMENTO ENCERRADO!          *' 00109200
                 DISPLAY '*                                          *' 00109300
                 DISPLAY '***************** EVSA0407 *****************' 00109400
              ELSE                                                      00109500
                 DISPLAY '***************** EVSA0407 ******************'00109600
                 DISPLAY '*                                           *'00109700
                 DISPLAY '*              EVSA0407  VAZIO              *'00109800
                 DISPLAY '*          PROCESSAMENTO  ENCERRADO         *'00109900
                 DISPLAY '*                                           *'00110000
                 DISPLAY '***************** EVSA0407 ******************'00110100
              END-IF                                                    00110200
           END-IF.                                                      00110300
                                                                        00110400
      *----------------------------------------------------------------*00110500
       2000-99-FIM.                         EXIT.                       00110600
      *----------------------------------------------------------------*00110700
                                                                        00110800
      ******************************************************************00110900
      *                     L E R    E V S A 0 4 0 7                   *00111003
      ******************************************************************00111100
                                                                        00111200
      *----------------------------------------------------------------*00111300
       2100-LER-EVSA0407                    SECTION.                    00111400
      *----------------------------------------------------------------*00111500
                                                                        00111600
           READ EVSA0407                    INTO REG-EVSA0407.          00111703
                                                                        00111800
           IF WRK-FS-EVSA                   EQUAL '10'                  00111900
              GO                            TO 2100-99-FIM              00112000
           END-IF                                                       00112100
                                                                        00112200
           MOVE WRK-LEITURA                 TO WRK-OPERACAO             00112300
           MOVE '2100'                      TO WRK-POSICAO              00112401
                                                                        00112500
           PERFORM 1100-TESTAR-FS                                       00112600
                                                                        00112700
           ADD 1                            TO ACU-LIDOS.               00112800
                                                                        00112900
           PERFORM 2110-CONSISTIR-EVSA0407.                             00113000
                                                                        00113100
           IF WRK-EVSA0407-INC              EQUAL 'S'                   00113201
              ADD 1                         TO ACU-INCON                00113300
              GO                            TO 2100-LER-EVSA0407        00113400
           END-IF.                                                      00113500
                                                                        00113600
      *----------------------------------------------------------------*00113700
       2100-99-FIM.                         EXIT.                       00113800
      *----------------------------------------------------------------*00113900
                                                                        00114000
      ******************************************************************00114100
      *          C O N S I S T I R    E V S A 0 4 0 7                  *00114201
      ******************************************************************00114300
                                                                        00114400
      *----------------------------------------------------------------*00114500
       2110-CONSISTIR-EVSA0407              SECTION.                    00114600
      *----------------------------------------------------------------*00114700
                                                                        00114800
           MOVE 'N'                         TO WRK-EVSA0407-INC         00114901
                                                                        00115200
           IF (EVSA-AGENCIA                 NOT NUMERIC   ) OR          00115301
              (EVSA-AGENCIA                 EQUAL ZEROS   )             00115401
              MOVE 'S'                      TO WRK-EVSA0407-INC         00115501
              GO                            TO 2110-99-FIM              00115601
           END-IF                                                       00115700
                                                                        00115800
           IF (EVSA-CONTA                    NOT NUMERIC  ) OR          00115901
              (EVSA-CONTA                    EQUAL ZEROS  )             00116001
              MOVE 'S'                       TO WRK-EVSA0407-INC        00116101
              GO                             TO 2110-99-FIM             00116201
           END-IF                                                       00116300
                                                                        00116400
           IF (EVSA-SALARIO                  NOT NUMERIC  ) OR          00116503
              (EVSA-CONTA                    EQUAL ZEROS  )             00116601
              MOVE 'S'                       TO WRK-EVSA0407-INC        00116701
           END-IF.                                                      00116901
                                                                        00118100
      *----------------------------------------------------------------*00118200
       2110-99-FIM.                         EXIT.                       00118300
      *----------------------------------------------------------------*00118400
                                                                        00118500
      ******************************************************************00118600
      *                       P R O C E S S A R                        *00118700
      ******************************************************************00118800
                                                                        00118900
      *----------------------------------------------------------------*00119000
       3000-PROCESSAR                       SECTION.                    00119100
      *----------------------------------------------------------------*00119200
                                                                        00119300
           PERFORM 3100-MOVER-SAIDA.                                    00120001
                                                                        00120101
           PERFORM 3200-GRAVAR-SAIDA.                                   00120201
                                                                        00120300
           PERFORM 2100-LER-EVSA0407.                                   00120400
                                                                        00120500
      *----------------------------------------------------------------*00120600
       3000-99-FIM.                         EXIT.                       00120700
      *----------------------------------------------------------------*00120800
                                                                        00120900
      ******************************************************************00121000
      *                  M O V E R   S A I D A                         *00121100
      ******************************************************************00121200
                                                                        00121300
      *----------------------------------------------------------------*00121400
       3100-MOVER-SAIDA                     SECTION.                    00121500
      *----------------------------------------------------------------*00121600
                                                                        00121700
           MOVE EVSA-AGENCIA                TO SVSA0407-DADOS-AGENCIA   00121801
           MOVE EVSA-CONTA                  TO SVSA0407-DADOS-CONTA     00121901
           MOVE EVSA-SALARIO                TO SVSA0407-DADOS-SALARIO   00122001
           MOVE WRK-DATA                    TO SVSA0407-DADOS-DATA.     00122104
                                                                        00122200
      *----------------------------------------------------------------*00122300
       3100-99-FIM.                         EXIT.                       00122400
      *----------------------------------------------------------------*00122500
                                                                        00122600
      ******************************************************************00122700
      *             G R A V A R   S A I D A - S V S A 0 4 0 7          *00122801
      ******************************************************************00122900
                                                                        00123000
      *----------------------------------------------------------------*00123100
       3200-GRAVAR-SAIDA                    SECTION.                    00123201
      *----------------------------------------------------------------*00123300
                                                                        00123400
           WRITE FD-SVSA0407                FROM WRK-CORPO              00123501
                                                                        00123600
           MOVE WRK-GRAVACAO                TO WRK-OPERACAO             00123700
           MOVE '3200'                      TO WRK-POSICAO              00123801
                                                                        00123900
           PERFORM 1120-TESTAR-FS-SVSA0407                              00124000
                                                                        00124100
           ADD 1                            TO ACU-GRAVS.               00124200
                                                                        00124300
      *----------------------------------------------------------------*00124400
       3200-99-FIM.                         EXIT.                       00124500
      *----------------------------------------------------------------*00124600
                                                                        00124700
      ******************************************************************00124800
      *                     F I N A L I Z A R                          *00124900
      ******************************************************************00125000
                                                                        00125100
      *----------------------------------------------------------------*00125200
       4000-FINALIZAR                       SECTION.                    00125300
      *----------------------------------------------------------------*00125400
                                                                        00125500
           IF ACU-LIDOS                     GREATER ZEROS               00125600
              PERFORM 4100-EMITIR-TOTAIS                                00125700
           END-IF                                                       00125800
                                                                        00125900
           CLOSE EVSA0407                                               00126000
                 SVSA0407                                               00126100
                                                                        00127600
           MOVE WRK-FECHAMENTO              TO WRK-OPERACAO             00127700
           MOVE '4000'                      TO WRK-POSICAO              00127801
                                                                        00127900
           PERFORM 1100-TESTAR-FS.                                      00128000
                                                                        00128100
      *----------------------------------------------------------------*00128200
       4000-99-FIM.                         EXIT.                       00128300
      *----------------------------------------------------------------*00128400
                                                                        00128500
      ******************************************************************00128600
      *                      R E S U L T A D O                         *00128700
      ******************************************************************00128800
                                                                        00128901
      *----------------------------------------------------------------*00129000
       4100-EMITIR-TOTAIS                   SECTION.                    00129100
      *----------------------------------------------------------------*00129200
                                                                        00129300
           DISPLAY '******************* MD020901 ********************'  00129400
           DISPLAY '*                                               *'  00129500
           DISPLAY '*         ARQUIVOS - PROCESSAMENTO OK!          *'  00129600
           DISPLAY '*                                               *'  00129700
           DISPLAY '* TOTAL LIDOS: ' ACU-LIDOS                          00129800
           DISPLAY '* TOTAL GRAVS: ' ACU-GRAVS                          00129900
           DISPLAY '* TOTAL INCON: ' ACU-INCON                          00130000
           DISPLAY '*                                               *'  00130100
           DISPLAY '******************* MD020901 ********************'. 00130200
                                                                        00130300
      *----------------------------------------------------------------*00130400
       4100-99-FIM.                         EXIT.                       00130500
      *----------------------------------------------------------------*00130600
                                                                        00130700
                                                                        00130800
      ******************************************************************00130900
      *                   T R A T A R   E R R O                        *00131000
      ******************************************************************00131100
      *----------------------------------------------------------------*00131200
       9000-TRATAR-ERROS                    SECTION.                    00131303
      *----------------------------------------------------------------*00131400
                                                                        00131500
           CALL WRK-GRVLOG                  USING WRK-LOG               00132204
           GOBACK.                                                      00132300
                                                                        00132400
      *----------------------------------------------------------------*00132500
       9000-99-FIM.                         EXIT.                       00132600
      *----------------------------------------------------------------*00133000
