      *================================================================*
       IDENTIFICATION                  DIVISION.
      *================================================================*

       PROGRAM-ID. MD020602.
       AUTHOR.     ACACIO.

      *================================================================*
      *                    T R E I N A M E N T O                       *
      *================================================================*
      *     PROGRAMA....: MD020901                                     *
      *     PROGRAMADOR.: ACACIO JORGE                                 *
      *     ANALISTA....: IVAN SANCHES                                 *
      *     DATA........: 06/07/2022                                   *
      *                                                                *
      *----------------------------------------------------------------*
      *                                                                *
      *     OBJETIVO: USAR SORT PARA FAZER LEITURA, GRAVAÇÃO, APAGAR   *
      *               E REGRAVAR ARQUIVO VSAM                          *
      *                                                                *
      *================================================================*

      *================================================================*
       ENVIRONMENT                     DIVISION.
      *================================================================*


      *----------------------------------------------------------------*
       INPUT-OUTPUT                    SECTION.
      *----------------------------------------------------------------*

       FILE-CONTROL.
            SELECT EVSA0407 ASSIGN TO EVSA0407
               ORGANIZATION  IS INDEXED
               ACCESS MODE   IS DYNAMIC
               RECORD KEY    IS ARQ-CHAVE
               FILE STATUS   IS FS-EVSA0407.

      *================================================================*
       DATA                            DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                            SECTION.
      *----------------------------------------------------------------*

       FD EVSA0407.
       01 REG-EVSA0407.
          05 ARQ-CHAVE.
             10 ARQ-AGENCIA PIC X(04).
             10 ARQ-CONTA   PIC X(05).
          05 ARQ-SALARIO    PIC X(10).

      *----------------------------------------------------------------*
       WORKING-STORAGE                      SECTION.
      *----------------------------------------------------------------*

       77 WRK-LEN     PIC 9999.
       77 FS-EVSA0407 PIC 9(02).

      *----------------------------------------------------------------*
       LINKAGE                         SECTION.
      *----------------------------------------------------------------*

       01 LNK-ENTRADA.
          05 LNK-LEN     PIC 9(04) COMP.
          05 LNK-CORPO.
             10 LNK-OPER    PIC X(01).
             10 LNK-CHAVE.
                15 LNK-AGENCIA PIC X(04).
                15 LNK-CONTA   PIC X(05).
             10 LNK-SAL     PIC X(10).

      *================================================================*
       PROCEDURE                       DIVISION       USING LNK-ENTRADA.
      *================================================================*

      *----------------------------------------------------------------*
       0000-PRINCIPAL                  SECTION.
      *----------------------------------------------------------------*

           PERFORM 1000-INICIAR.
           PERFORM 2000-PROCESSAR.

           PERFORM 3000-FINALIZAR.
           GOBACK.

      *----------------------------------------------------------------*
       0000-99-FIM.                    EXIT.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       1000-INICIAR                    SECTION.
      *----------------------------------------------------------------*

           OPEN I-O EVSA0407.

           IF FS-EVSA0407 NOT EQUAL ZEROS
              DISPLAY 'ERRO OPEN'
              GOBACK
           END-IF.

      *----------------------------------------------------------------*
       1000-99-FIM.                    EXIT.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       2000-PROCESSAR                  SECTION.
      *----------------------------------------------------------------*

           EVALUATE LNK-OPER
             WHEN 'L'
                PERFORM 2100-LEITURA
             WHEN 'G'
                PERFORM 2200-GRAVACAO
             WHEN 'D'
                PERFORM 2300-DELETAR
             WHEN 'R'
                PERFORM 2400-ATUALIZAR
           END-EVALUATE.

      *----------------------------------------------------------------*
       2000-99-FIM.                    EXIT.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       2100-LEITURA                    SECTION.
      *----------------------------------------------------------------*

            MOVE LNK-AGENCIA TO ARQ-AGENCIA.
            MOVE LNK-CONTA   TO ARQ-CONTA.
            READ EVSA0407
            IF FS-EVSA0407 EQUAL ZEROS
               DISPLAY REG-EVSA0407
            ELSE
               DISPLAY 'NAO ENCONTRADO O REGISTRO'
            END-IF.

      *----------------------------------------------------------------*
       2100-99-FIM.                    EXIT.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       2200-GRAVACAO                   SECTION.
      *----------------------------------------------------------------*

           MOVE LNK-AGENCIA TO ARQ-AGENCIA.
           MOVE LNK-CONTA   TO ARQ-CONTA.
           MOVE LNK-SAL     TO ARQ-SALARIO.
           WRITE REG-EVSA0407
           IF FS-EVSA0407 EQUAL ZEROS
              DISPLAY REG-EVSA0407
           ELSE
              DISPLAY 'NAO GRAVADO'
           END-IF.

      *----------------------------------------------------------------*
       2200-99-FIM.                    EXIT.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       2300-DELETAR                    SECTION.
      *----------------------------------------------------------------*

           MOVE LNK-AGENCIA            TO ARQ-AGENCIA.
           MOVE LNK-CONTA              TO ARQ-CONTA.
           READ EVSA0407
           IF FS-EVSA0407 EQUAL ZEROS
              DELETE EVSA0407
              IF FS-EVSA0407 EQUAL ZEROS
                 DISPLAY 'ARQUIVO DELETADO'
              END-IF
           ELSE
              DISPLAY 'NAO ENCONTRADO'
           END-IF.

      *----------------------------------------------------------------*
       2300-99-FIM.                    EXIT.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       2400-ATUALIZAR                  SECTION.
      *----------------------------------------------------------------*

           MOVE LNK-AGENCIA            TO ARQ-AGENCIA.
           MOVE LNK-CONTA              TO ARQ-CONTA.
           READ EVSA0407
           IF FS-EVSA0407              EQUAL ZEROS
              MOVE LNK-SAL             TO ARQ-SALARIO
              REWRITE REG-EVSA0407
              IF FS-EVSA0407           EQUAL ZEROS
                 DISPLAY REG-EVSA0407 'ATUALIZADO'
              END-IF
           ELSE
              DISPLAY 'NAO ATUALIZADO'
           END-IF.

      *----------------------------------------------------------------*
       2400-99-FIM.                    EXIT.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       3000-FINALIZAR                  SECTION.
      *----------------------------------------------------------------*

           CLOSE EVSA0407.

      *----------------------------------------------------------------*
       3000-99-FIM.                    EXIT.
      *----------------------------------------------------------------*
